;--------------------------
; 大破艦修復メニュー
;--------------------------
@SLG_REPAIR_WRECKED_SHIP(PORT_POSID, OWNER, LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE)
#DIM PORT_POSID
#DIM OWNER
#DIMS REF LEFT_COLUMNS
#DIM REF LEFT_COLUMNS_BUTTON_VALUE
#DIM LINE_ITER
#DIM LEFT_COLUMN_SIZE
#DIM 入力値
#DIM SHIP_STATUS, SHIP_STATUS_LENGTH
#DIM SHIP_ID

FINDELEMENT LEFT_COLUMNS_BUTTON_VALUE, NONE_LINE
LEFT_COLUMN_SIZE = RESULT
$INPUT_LOOP
LINE_ITER = 0

CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
CALL DRAW_RIGHT_COLUMN_GRAY_LINE
LINE_ITER ++

CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
IF MONEY == 0
    LOCALS = $0
ELSE
    LOCALS = %TOSTR(MONEY, "$###,###,###,###")%
ENDIF
PRINTFORML 大破艦修復 宇宙港に停泊中の大破した艦を修復できます - 所持金: %LOCALS%
LINE_ITER ++

CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
CALL DRAW_RIGHT_COLUMN_GRAY_LINE
LINE_ITER ++

; 艦船一覧
FOR LOCAL, 0, MAX_SHIP
    SIF SHIP_COUNTRY:LOCAL != 国家ID_プレイヤー
        CONTINUE
    SIF SHIP_POSITION:LOCAL != PORT_POSID
        CONTINUE
    SIF SHIP_CONDITION:LOCAL != 艦船状態_大破
        CONTINUE
    CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
    IF SHIP_MODEL:LOCAL >= SHIP_MODEL_BUFFER
        LOCAL:1 = ORIGIN_SHIP_PRICE:(SHIP_MODEL:LOCAL - SHIP_MODEL_BUFFER)
    ELSE
        TRYCALLFORM GET_SHIP_STATUS_{SHIP_MODEL:LOCAL}(SHIP_STATUS)
        LOCAL:1 = SHIP_STATUS:SHIP_PRICE
    ENDIF 
    TIMES LOCAL:1, 0.7
    LOCALS = %TOSTR(LOCAL:1, "$###,###,###,###")%
    PRINTBUTTON @"%SHIP_NAME:LOCAL, 30, LEFT% %LOCALS%", LOCAL + 100
    PRINTL
    LINE_ITER++
NEXT

; 左カラムが右カラムより長かった場合の処理
FOR LOCAL, LINE_ITER, LEFT_COLUMN_SIZE
    CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
    PRINTL
    LINE_ITER ++
NEXT


INPUT
IF RESULT >= 0 && RESULT < LEFT_COLUMN_SIZE
    RETURN RESULT
ENDIF
入力値 = RESULT - 100
IF 入力値 >= 0 && 入力値 < MAX_SHIP
    SIF SHIP_COUNTRY:入力値 != 国家ID_プレイヤー
        GOTO INPUT_LOOP
    SIF SHIP_POSITION:入力値 != PORT_POSID
        GOTO INPUT_LOOP
    SIF SHIP_CONDITION:入力値 != 艦船状態_大破
        GOTO INPUT_LOOP
    PRINTL
    PRINTFORML %SHIP_NAME:入力値%を修復しますか?
    CALL ASK_YN
    SIF RESULT == 1
        GOTO INPUT_LOOP
    IF MONEY < LOCAL:1
        PRINTFORML 所持金が足りません
        GOTO INPUT_LOOP 
    ENDIF
    SHIP_CONDITION:入力値 = 0
    SHIP_NAME:入力値 = %REPLACE(SHIP_NAME:入力値, "<大破>", "")%
    MONEY -= LOCAL:1
    SIF OWNER != 国家ID_プレイヤー
        MONEY:OWNER += LOCAL:1
    PRINTFORMW %SHIP_NAME:入力値%の修復が完了しました！
ENDIF
GOTO INPUT_LOOP
RETURN