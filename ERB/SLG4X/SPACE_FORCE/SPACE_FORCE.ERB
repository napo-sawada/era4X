;------------------------
; 宇宙軍
;------------------------

;------------------------
; 宇宙軍基地
;------------------------
@SPACE_FORCE_BASE(COUNTRY_ID = 国家ID_プレイヤー)
#DIM COUNTRY_ID
#DIM _CURRENT_OCCUPANCY_POINT
#DIM _TOTAL_OCCUPANCY_POINT

$INPUT_LOOP
_CURRENT_OCCUPANCY_POINT = GET_CURRENT_OCCUPANCY_POINT(COUNTRY_ID)
_TOTAL_OCCUPANCY_POINT = GET_MAX_OCCUPANCY_POINT(COUNTRY_ID)
PRINTFORM ■宇宙軍 許容量 
IF _CURRENT_OCCUPANCY_POINT > _TOTAL_OCCUPANCY_POINT
  SETCOLOR 0XFF0000
ELSE
  SETCOLOR 0X00FF00
ENDIF
PRINTFORML ({_CURRENT_OCCUPANCY_POINT}/{_TOTAL_OCCUPANCY_POINT})
RESETCOLOR
PRINTFORML 宇宙軍基地レベル: {GET_STAR_FORCE_BASE_LEVEL()}
SETCOLOR 0X666666
CUSTOMDRAWLINE ///
RESETCOLOR
PRINTFORML 艦種: %"コルベット", 20, LEFT% 使用許容量: {GET_CURRENT_OCCUPANCY_POINT(COUNTRY_ID, コルベット)}({RESULT:1}隻)
PRINTFORML 艦種: %"フリゲート", 20, LEFT% 使用許容量: {GET_CURRENT_OCCUPANCY_POINT(COUNTRY_ID, フリゲート)}({RESULT:1}隻)
PRINTFORML 艦種: %"駆逐艦", 20, LEFT% 使用許容量: {GET_CURRENT_OCCUPANCY_POINT(COUNTRY_ID, 駆逐艦)}({RESULT:1}隻)
PRINTFORML 艦種: %"巡洋艦", 20, LEFT% 使用許容量: {GET_CURRENT_OCCUPANCY_POINT(COUNTRY_ID, 巡洋艦)}({RESULT:1}隻)
PRINTFORML 艦種: %"戦艦", 20, LEFT% 使用許容量: {GET_CURRENT_OCCUPANCY_POINT(COUNTRY_ID, 戦艦)}({RESULT:1}隻)
PRINTFORML 艦種: %"空母", 20, LEFT% 使用許容量: {GET_CURRENT_OCCUPANCY_POINT(COUNTRY_ID, 空母)}({RESULT:1}隻)
SETCOLOR 0X666666
CUSTOMDRAWLINE ///
RESETCOLOR
PRINTBUTTON "[投資]", 0
PRINT  
PRINTBUTTON "[戻る]", 1
PRINTL
SETCOLOR 0X666666
CUSTOMDRAWLINE ///
RESETCOLOR
INPUT
IF RESULT == 0
  CALL DONATE_SPACE_FORCE_BASE
ELSEIF RESULT == 1
  RETURN
ELSE
  GOTO INPUT_LOOP
ENDIF
;------------------------
; 現状の宇宙軍許容量の使用量を返却
;------------------------
@GET_CURRENT_OCCUPANCY_POINT(COUNTRY_ID = 国家ID_プレイヤー, TARGET_CATEGORY = -1)
#FUNCTION
#DIM COUNTRY_ID
#DIM TARGET_CATEGORY
#DIM _SHIP_ID
#DIM DYNAMIC _TOTAL_OCCUPANCY_POINT

VARSET LOCAL, 0
VARSET RESULT, 0

FOR _SHIP_ID, 0, MAX_SHIP
  SIF TARGET_CATEGORY != -1 && SHIP_CATEGORY:_SHIP_ID != TARGET_CATEGORY
    CONTINUE
  IF SHIP_COUNTRY:_SHIP_ID == COUNTRY_ID
    _TOTAL_OCCUPANCY_POINT += OCCUPANCY_POINT:(SHIP_CATEGORY:_SHIP_ID)
    LOCAL:(SHIP_CATEGORY:_SHIP_ID) ++
	ENDIF
NEXT

IF LOCAL:コルベット > 3
  _TOTAL_OCCUPANCY_POINT -= LOCAL:コルベット * OCCUPANCY_POINT:コルベット / 3
ENDIF

IF LOCAL:フリゲート > 3
  _TOTAL_OCCUPANCY_POINT -= LOCAL:フリゲート * OCCUPANCY_POINT:フリゲート / 3
ENDIF

_TOTAL_OCCUPANCY_POINT -= LOCAL:駆逐艦 * OCCUPANCY_POINT:駆逐艦 / 3

_TOTAL_OCCUPANCY_POINT -= LOCAL:巡洋艦 * OCCUPANCY_POINT:巡洋艦 / 3

_TOTAL_OCCUPANCY_POINT -= LOCAL:戦艦 * OCCUPANCY_POINT:戦艦 / 3

_TOTAL_OCCUPANCY_POINT -= LOCAL:空母 * OCCUPANCY_POINT:空母 / 3

SIF TARGET_CATEGORY != -1
  RESULT:1 = LOCAL:TARGET_CATEGORY
RETURNF _TOTAL_OCCUPANCY_POINT

;------------------------
; 現状の宇宙軍許容量の限界を返却
;------------------------
@GET_MAX_OCCUPANCY_POINT(COUNTRY_ID = 国家ID_プレイヤー)
#FUNCTION
#DIM COUNTRY_ID
#DIM _MAX_OCCUPANCY_POINT

; 基礎値
_MAX_OCCUPANCY_POINT = 30

; 宇宙軍のレベル
IF SPACE_FORCE_BUILD
  ; レベル0
  _MAX_OCCUPANCY_POINT += 10

  ; レベル1
  SIF SPACE_FORCE_TOTAL_DONATE >= 10000000 && IS_COMPLETE_TECH:0
    _MAX_OCCUPANCY_POINT += 10

  ; レベル2
  SIF SPACE_FORCE_TOTAL_DONATE >= 100000000 && IS_COMPLETE_TECH:8
    _MAX_OCCUPANCY_POINT += 10

  ; レベル3
  SIF SPACE_FORCE_TOTAL_DONATE >= 500000000 && IS_COMPLETE_TECH:14
    _MAX_OCCUPANCY_POINT += 12

  ; レベル4
  SIF SPACE_FORCE_TOTAL_DONATE >= 1200000000 && IS_COMPLETE_TECH:20
    _MAX_OCCUPANCY_POINT += 16

  ; レベル5
  SIF SPACE_FORCE_TOTAL_DONATE >= 330000000 && IS_COMPLETE_TECH:27
    _MAX_OCCUPANCY_POINT += 16

  ; レベル6
  SIF SPACE_FORCE_TOTAL_DONATE >= 660000000 && IS_COMPLETE_TECH:34
    _MAX_OCCUPANCY_POINT += 16

  ; レベル7
  SIF SPACE_FORCE_TOTAL_DONATE >= 1000000000 && IS_COMPLETE_TECH:39
    _MAX_OCCUPANCY_POINT += 32
ENDIF

RETURNF _MAX_OCCUPANCY_POINT

;------------------------
; 現状の宇宙軍レベルを返却
;------------------------
@GET_STAR_FORCE_BASE_LEVEL
#FUNCTION
SIF SPACE_FORCE_TOTAL_DONATE >= 1000000000 && IS_COMPLETE_TECH:39
  RETURNF 7
SIF SPACE_FORCE_TOTAL_DONATE >= 660000000 && IS_COMPLETE_TECH:34
  RETURNF 6
SIF SPACE_FORCE_TOTAL_DONATE >= 330000000 && IS_COMPLETE_TECH:27
  RETURNF 5
SIF SPACE_FORCE_TOTAL_DONATE >= 1200000000 && IS_COMPLETE_TECH:20
  RETURNF 4
SIF SPACE_FORCE_TOTAL_DONATE >= 500000000 && IS_COMPLETE_TECH:14
  RETURNF 3
SIF SPACE_FORCE_TOTAL_DONATE >= 100000000 && IS_COMPLETE_TECH:8
  RETURNF 2
SIF SPACE_FORCE_TOTAL_DONATE >= 10000000 && IS_COMPLETE_TECH:0
  RETURNF 1
RETURNF 0

;------------------------
; 宇宙軍許容量を超えていた時のペナルティを返却
;------------------------
@GET_SPACE_FORCE_OCCUPANCY_PENALTY
#FUNCTION

RETURNF MAX(0, GET_CURRENT_OCCUPANCY_POINT() - GET_MAX_OCCUPANCY_POINT()) * PENALTY

;------------------------
; 宇宙軍基地に投資
;------------------------
@DONATE_SPACE_FORCE_BASE
#DIM 投資額

$INPUT_LOOP
LOCAL = SPACE_FORCE_TOTAL_DONATE
IF LOCAL == 0
  LOCALS = $0
ELSE
  LOCALS = %TOSTR(LOCAL, "$###,###")%
ENDIF
PRINTFORML 現在の投資額: %LOCALS% 宇宙軍基地レベル: {GET_STAR_FORCE_BASE_LEVEL()}
SELECTCASE GET_STAR_FORCE_BASE_LEVEL()
  CASE 0
    PRINTFORML 次のレベルまで: 投資: %TOSTR(10000000, "$###,###")% 研究: コルベット開発
  CASE 1
    PRINTFORML 次のレベルまで: 投資: %TOSTR(100000000, "$###,###")% 研究: フリゲート開発
  CASE 2
    PRINTFORML 次のレベルまで: 投資: %TOSTR(500000000, "$###,###")% 研究: 駆逐艦開発
  CASE 3
    PRINTFORML 次のレベルまで: 投資: %TOSTR(1200000000, "$###,###")% 研究: 巡洋艦開発
  CASE 4
    PRINTFORML 次のレベルまで: 投資: %TOSTR(330000000, "$###,###")% 研究: 戦艦開発
  CASE 5
    PRINTFORML 次のレベルまで: 投資: %TOSTR(660000000, "$###,###")% 研究: 空母開発
  CASE 6
    PRINTFORML 次のレベルまで: 投資: %TOSTR(1000000000, "$###,###")% 研究: サイズ5装備開発
  CASEELSE
    PRINTFORML すでに最大レベルです
ENDSELECT

PRINTFORML 投資額を入力してください(0でキャンセル)
INPUT
投資額 = RESULT
SIF 投資額 == 0
  RETURN
IF 投資額 > MONEY
  PRINTFORMW 所持金を超えています
  GOTO INPUT_LOOP
ENDIF
PRINTFORML %TOSTR(投資額, "$###,###")%投資しますか?
CALL ASK_YN
SIF RESULT
  GOTO INPUT_LOOP
SPACE_FORCE_TOTAL_DONATE += 投資額
MONEY -= 投資額
GOTO INPUT_LOOP
