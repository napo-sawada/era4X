;--------------------------
; !!異端狩りと 28を好きな番号に変える！!
; 異端狩り
; ここにクエストの説明
;--------------------------


;--------------------------
; クエスト受注可能か
;--------------------------
@QUEST_ACCEPTABLE_28
CALL GET_STAR_ID_FROM_POS(CHARACTER_POSITION)
SIF STAR_OWNER:RESULT != 国家ID_アレス宗教
  RETURN 0
SIF QUEST_COMPLETE_DATE:28 + 100 > DAY || QUEST_PROGRESS:28 == 1
  RETURN 0
RETURN 1

;--------------------------
; クエストの名前を返却
;--------------------------
@QUEST_NAME_28
RESULTS = 異端狩り
RETURN


;--------------------------
; マップでの表示を設定する
; @QUEST_SET_TILE_  : マップ上対象地点にマークを出す。
; @QUEST_MAP_INFO_  : 右下小窓にメッセージを出す
; 引数１： ARG:0 SS28YY　座標データ　マップ上でクリックされた座標。表示条件分岐に。
;--------------------------
;;;マップ画面上での表示。 隠したかったら関数コメントアウトか RETURN 0
;;;色を替えたかったらSETCOLORしておけば後の処理で色反映します
@QUEST_SET_TILE_28 (ARG:0 )
	RESULTS '= "！"
	SETCOLOR 0xFFFF00
RETURN 1

;;;右下小窓に出す。隠したければ関数コメントアウトか RETURN 0
@QUEST_MAP_INFO_28 (ARG:0 )
		RESULTS:0 '= "異端宙族基地"
RETURN 1

;-----------------------
; マップ上からQUEST_BODYを実行する処理
; @QUEST_SHOW_COMMAND_x : ここで指定した文字列のコマンドを現地で表示する
; 　　　　　　　　　　 ：(調査する||捜索する||採集する||待ち伏せする||挑戦する)といったフレーバー
; @QUEST_PLACE_CHECK_x ： 設定マスを踏んだら呼び出される。不要ならば消すか無処理で返す
;--------------------------
@QUEST_SHOW_COMMAND_28 ()
	RESULTS '= "クエストを行う"
RETURN 1

@QUEST_REACH_PLACE_28 ()
; CALL QUEST_BODY_28
RETURN

;--------------------------
; クエスト本体
;--------------------------
@QUEST_BODY_28

#DIM CONST QUEST_ID = 28
#DIM SECTOR_ID
#DIM X_POS
#DIM Y_POS
#DIM SHIP_ID
#DIM POSITION_ID
#DIM 民主主義好感度

CALL ID2POS(CHARACTER_POSITION)
SECTOR_ID = RESULT:0
X_POS = RESULT:1
Y_POS = RESULT:2
POSITION_ID = CHARACTER_POSITION

;;宙域図から呼び出されたらこちら　クエスト進行中
IF QUESTMAP:(RESULT:0):(RESULT:1):(RESULT:2) == QUEST_ID
	QUEST_FLAG_SHIP_ID:QUEST_ID = -1
	PRINTFORML 目的地の岩石地帯に、うまく隠蔽された宙族の基地を発見した
	PRINTFORML 早速攻撃しよう
	FOR SHIP_ID, 0, 3
		CALL INIT_PRESET_SHIP(国家ID_民主主義, CHARACTER_POSITION, 汎用コルベット, "護衛コルベット", 汎用コルベット)
	NEXT
	CALL INIT_PRESET_SHIP(国家ID_民主主義, CHARACTER_POSITION, 汎用フリゲート, "護衛フリゲート", 汎用フリゲート)
	QUEST_FLAG_SHIP_ID:QUEST_ID = RESULT
	SHIP_EQUIP:主砲:0:RESULT = 12
	SHIP_EQUIP:装甲:0:RESULT = 9
	SHIP_EQUIP:装甲:1:RESULT = 9
	SHIP_EQUIP:シールド:0:RESULT = 9
	SHIP_EQUIP:シールド:1:RESULT = 9
	PRINTFORMW %CALLNAME:MASTER%の命令一下、艦隊が戦闘態勢に入った
	民主主義好感度 = REL_LIKE_COUNTRY:国家ID_民主主義:国家ID_プレイヤー
	REL_LIKE_COUNTRY:国家ID_民主主義:国家ID_プレイヤー = -1000
	CALL COMBAT(CHARACTER_POSITION)
	CALL RETIRE_BROKEN_SHIP
	REL_LIKE_COUNTRY:国家ID_民主主義:国家ID_プレイヤー = 民主主義好感度
	IF SHIP_COUNTRY:(QUEST_FLAG_SHIP_ID:QUEST_ID) != 国家ID_民主主義
		;; 敵の旗艦を撃破し、自分が撃破されていない
		IF CHARACTER_POSITION == POSITION_ID
			QUEST_PROGRESS:QUEST_ID = 2
			PRINTFORML 敵の旗艦をデブリと化すことができた。ミッション完了だ
			PRINTFORMW クエスト窓口にミッション完了の報告に行こう
		ELSE
			QUEST_PROGRESS:QUEST_ID = 0
		ENDIF
		;; 引き分け。互いに旗艦が生存している状態
	ELSEIF CHARACTER_POSITION == POSITION_ID
		PRINTFORMW ミッション失敗だ。ただ命があることを神に感謝しよう
		QUEST_PROGRESS:QUEST_ID = 0
	ENDIF
	FOR SHIP_ID, 0, MAX_SHIP
		IF SHIP_COUNTRY:SHIP_ID == 国家ID_民主主義 && SHIP_POSITION:SHIP_ID == POSITION_ID
			CALL GET_SHIP_TOTAL_HP(SHIP_ID)
			SHIP_DAMAGE:SHIP_ID = RESULT
		ENDIF
	NEXT
	CALL RETIRE_BROKEN_SHIP
	CALL QUEST_RELEASE_POINT(QUEST_ID, CHARACTER_POSITION:MASTER)
ELSEIF QUEST_PROGRESS:QUEST_ID == 2
	PRINTFORML クエスト窓口でミッション完了の報告をした
	PRINTFORML これにより報酬と宗教国家の評価を得ることができた
	PRINTFORML 所持金+$250,000
	PRINTFORMW 宗教国家の評価+10
	MONEY += 250000
	MONEY:国家ID_アレス宗教 -= 250000
	REL_LIKE_COUNTRY:国家ID_アレス宗教:国家ID_プレイヤー = LIMIT(REL_LIKE_COUNTRY:国家ID_アレス宗教:国家ID_プレイヤー + 10, -1000, 1000)
	QUEST_PROGRESS:QUEST_ID = 0
	QUEST_COMPLETE_DATE:QUEST_ID = DAY
ELSE
	PRINTFORML 「今回のミッションは異端の宙族共を撃破することにあります」
	PRINTFORMW 「こざかしくも敵は重武装したフリゲートを旗艦にした小艦隊で構成されており、即応できる偵察分隊レベルだと被害が出るため、うかつに手が出せないのです」
	PRINTL
	PRINTFORML 「そこで%CALLNAME:MASTER%殿にはこの異端宇宙族を撃破してほしいのです。頼みますぞ」
	PRINTW ………………
	PRINTW …………
	PRINTW ……
	PRINTW 指定座標に×印が付けられた。早急に向かおう。
	LOCAL = 1
	WHILE LOCAL
		;; 関数つかって適当に。 指定セクター内　6:ちょっと外より を抽選
		CALL QUEST_SET_RANDPOINT(QUEST_ID, SECTOR_ID, 6 )
		IF RESULT >= 0
			X_POS = (RESULT / 100) %100
			Y_POS = RESULT %100
			CLEARMAP:SECTOR_ID:X_POS:Y_POS = 1		;;明瞭化おまけ
			LOCAL --
		ENDIF
	WEND
	QUEST_PROGRESS:QUEST_ID = 1
ENDIF