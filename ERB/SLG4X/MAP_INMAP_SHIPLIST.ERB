;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;@CREATE_WINDOW_INMAP_SHIPLIST
;;; 保有している艦船の一覧をマップ上に表示する
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
@CREATE_WINDOW_INMAP_SHIPLIST
#DIM DYNAMIC  WINDOW_ID
#DIM CONST WINDOW_WIDTH = 108
#DIM CONST WINDOW_HEIGHT = 33

#DIM DYNAMIC L_CNT
#DIM DYNAMIC PAGE
#DIM DYNAMIC MAX_PAGE
#DIM DYNAMIC SCROLL_IDX_COL
#DIM DYNAMIC SORTLIST, MAX_SHIP
#DIM DYNAMIC SORTMODE
;ゲーム中はフィルター内容を保持したいのでFILTER_LISTはDYNAMICで宣言しない
#DIM FILTER_LIST, SHIP_CATEGORY_COUNT
#DIM CONST FILTER_BTN = 100

#DIM DYNAMIC SHIP_ID
#DIM DYNAMIC SHIPLIST_IDX
#DIMS DYNAMIC  所有船リスト
#DIMS DYNAMIC 艦船データ, MAX_SHIP
#DIM DYNAMIC FIREPOWER_LIST, 6

#DIMS CONST HEADER_ITEM = "艦名", "艦種", "耐久", "ｼｰﾙﾄﾞ", "座標", "任務", "実体弾", "ﾚｰｻﾞｰ  ", "ﾐｻｲﾙ  ", "航空  ", "雷撃  ", "ﾋﾞｰﾑ   ", "対空  ", "回避  " 
#DIM CONST WIDTH_ITEM = 22, 10, 16, 16, 10, 8, 8, 8, 8, 8, 8, 8, 8, 8
#DIM CONST MAX_NUM_LIST = 23
#DIM DYNAMIC WIDTH_ITEM_TOTAL
#DIM DYNAMIC NUM_PRINT_ITEM

#DIM CONST 項目_艦名 = 0
#DIM CONST 項目_艦種 = 1
#DIM CONST 項目_耐久 = 2
#DIM CONST 項目_シールド = 3
#DIM CONST 項目_座標 = 4
#DIM CONST 項目_任務 = 5
#DIM CONST 項目_実体弾 = 6
#DIM CONST 項目_レーザー = 7
#DIM CONST 項目_ミサイル = 8
#DIM CONST 項目_航空 = 9
#DIM CONST 項目_雷撃 = 10
#DIM CONST 項目_ビーム = 11
#DIM CONST 項目_対空 = 12
#DIM CONST 項目_回避 = 13

;全OFFの場合のみ1で初期化する
IF FINDELEMENT(FILTER_LIST, 1) == -1
	VARSET FILTER_LIST, 1
ENDIF

;ウィンドウ初期化
CALL WndMgr_DestroyWindow(-1)
;ウィンドウ生成
CALL WndMgr_VacantId()
WINDOW_ID = RESULT
CALL WndMgr_CreateWindow(WINDOW_ID, 1, 2, WINDOW_WIDTH + 2, WINDOW_HEIGHT, 2)
CALL WndMgr_WindowTitle(WINDOW_ID, "保有艦船一覧")

CALL WndMgr_DisplayAll()

FOR SHIP_ID, 0, MAX_SHIP
	IF SHIP_COUNTRY:SHIP_ID == 国家ID_プレイヤー
		所有船リスト = %LIST_ADD(所有船リスト, TOSTR(SHIP_ID))%
	ENDIF
NEXT

FOR SHIPLIST_IDX, 0, LIST_COUNT(所有船リスト) + 1
	;データ最後尾は末尾を示す-1を付加
	IF SHIPLIST_IDX == LIST_COUNT(所有船リスト)
		艦船データ:SHIPLIST_IDX = "-1"
		BREAK
	ENDIF
	SHIP_ID = TOINT(LIST_GET(所有船リスト, SHIPLIST_IDX))
	CALL GET_SHIP_STATUS_BY_DIC(SHIP_ID)
	艦船データ:SHIPLIST_IDX = %RESULTS%
NEXT

WHILE 1
;-------------------------------------------------
;画面更新処理
;-------------------------------------------------
	REDRAW 0
	CALL WndMgr_ResetWindow(WINDOW_ID)
	CALL WndMgr_WindowTitle(WINDOW_ID, "保有艦船一覧")
	CLEARLINE WINDOW_HEIGHT + 3
	L_CNT = 0
	;;ヘッダー出力
	;-------------------------------------------------
	;艦名と艦種は固定で出力
	WIDTH_ITEM_TOTAL = 0
	LOCALS = %FuncTagSetText_ShapeSize(@"@B:{項目_艦名}@%HEADER_ITEM:項目_艦名%@/B@", WIDTH_ITEM:項目_艦名, 1)%
	WIDTH_ITEM_TOTAL += WIDTH_ITEM:項目_艦名
	LOCALS += FuncTagSetText_ShapeSize(@"@B:{項目_艦種}@%HEADER_ITEM:項目_艦種%@/B@", WIDTH_ITEM:項目_艦種, 1)
	WIDTH_ITEM_TOTAL += WIDTH_ITEM:項目_艦種
	LOCALS += "@B:A@<<@/B@  "
	NUM_PRINT_ITEM = 2
	FOR LOCAL, 2 + SCROLL_IDX_COL, VARSIZE("HEADER_ITEM")
		LOCALS += FuncTagSetText_ShapeSize(@"@B:{LOCAL}@%HEADER_ITEM:LOCAL%@/B@", WIDTH_ITEM:LOCAL, 1, 2)
		WIDTH_ITEM_TOTAL += WIDTH_ITEM:LOCAL
		NUM_PRINT_ITEM ++
		SIF WIDTH_ITEM_TOTAL > WINDOW_WIDTH - 15
			BREAK
	NEXT
	LOCALS += FuncTagSetText_ShapeSize("", (WINDOW_WIDTH - WIDTH_ITEM_TOTAL) - 10, 1, 1)
	LOCALS += "@B:D@>>@/B@" 
	CALL WndMgr_TextSet(WINDOW_ID, L_CNT, @"%LOCALS%")
	L_CNT ++
	CALL WndMgr_TextSet(WINDOW_ID, L_CNT++, @"@C:{カラー_選択不可}@―@/C@" * (WINDOW_WIDTH / 2))
	;;艦船数とフィルター内容から必要なページ数を算出
	;-------------------------------------------------
	VARSET LOCAL
	FOR LOCAL, 0, LIST_COUNT(所有船リスト)
		SIF FILTER_LIST:TOINT(DIC_GET(艦船データ:LOCAL, "艦種")) == 0
			CONTINUE
		LOCAL:1 ++
	NEXT
	IF LOCAL:1 > MAX_NUM_LIST
		MAX_PAGE = LOCAL:1 / MAX_NUM_LIST
	ELSE
		MAX_PAGE = 0
	ENDIF
	;;艦船一覧出力
	;-------------------------------------------------
	FOR SHIPLIST_IDX, PAGE * MAX_NUM_LIST, LIST_COUNT(所有船リスト)
		;表示可能領域を超える場合は出力を止める
		SIF SHIPLIST_IDX > MAX_NUM_LIST
			BREAK
		;フィルター対象の艦種は出力を省略
		SIF FILTER_LIST:TOINT(DIC_GET(艦船データ:SHIPLIST_IDX, "艦種")) == 0
			CONTINUE
		LOCALS = %FuncTagSetText_ShapeSize(DIC_GET(艦船データ:SHIPLIST_IDX, "艦名"), WIDTH_ITEM:項目_艦名, 1)%
		LOCALS += FuncTagSetText_ShapeSize(DIC_GET(艦船データ:SHIPLIST_IDX, "艦種名"), WIDTH_ITEM:項目_艦種, 1)
		LOCALS += "    "
		FOR LOCAL, 2 + SCROLL_IDX_COL, NUM_PRINT_ITEM + SCROLL_IDX_COL
			SELECTCASE LOCAL
				CASE 項目_耐久
					LOCALS:1 = %FuncTagSetText_ShapeSize(DIC_GET(艦船データ:SHIPLIST_IDX, "現HP"), 6, 0)%
					LOCALS:1 += " / " + DIC_GET(艦船データ:SHIPLIST_IDX, "最大HP")
					LOCALS += FuncTagSetText_ShapeSize(LOCALS:1, WIDTH_ITEM:項目_耐久, 1, 2)
				CASE 項目_シールド
					LOCALS:1 = %FuncTagSetText_ShapeSize(DIC_GET(艦船データ:SHIPLIST_IDX, "現シールド"), 6, 0)%
					LOCALS:1 += " / " + DIC_GET(艦船データ:SHIPLIST_IDX, "最大シールド")
					LOCALS += FuncTagSetText_ShapeSize(LOCALS:1, WIDTH_ITEM:項目_シールド, 1, 2)
				CASE 項目_座標
					LOCALS:1 = %REPLACE(DIC_GET(艦船データ:SHIPLIST_IDX, "座標"),"(..)(..)(..)", "#$1\($2,$3\)")%
					LOCALS += FuncTagSetText_ShapeSize(LOCALS:1, WIDTH_ITEM:項目_座標, 1, 2)
				CASE 項目_任務
					LOCALS += FuncTagSetText_ShapeSize(DIC_GET(艦船データ:SHIPLIST_IDX, "任務名"), WIDTH_ITEM:項目_任務, 1, 2)
				CASE 項目_実体弾
					LOCALS:1 = %LIST_GET(DIC_GET(艦船データ:SHIPLIST_IDX, "合計火力リスト"), 0)%
					LOCALS += FuncTagSetText_ShapeSize(LOCALS:1, WIDTH_ITEM:項目_実体弾, 1, 0)
				CASE 項目_レーザー
					LOCALS:1 = %LIST_GET(DIC_GET(艦船データ:SHIPLIST_IDX, "合計火力リスト"), 1)%
					LOCALS += FuncTagSetText_ShapeSize(LOCALS:1, WIDTH_ITEM:項目_レーザー, 1, 0)
				CASE 項目_ミサイル
					LOCALS:1 = %LIST_GET(DIC_GET(艦船データ:SHIPLIST_IDX, "合計火力リスト"), 2)%
					LOCALS += FuncTagSetText_ShapeSize(LOCALS:1, WIDTH_ITEM:項目_ミサイル, 1, 0)
				CASE 項目_航空
					LOCALS:1 = %LIST_GET(DIC_GET(艦船データ:SHIPLIST_IDX, "合計火力リスト"), 3)%
					LOCALS += FuncTagSetText_ShapeSize(LOCALS:1, WIDTH_ITEM:項目_航空, 1, 0)
				CASE 項目_雷撃
					LOCALS:1 = %LIST_GET(DIC_GET(艦船データ:SHIPLIST_IDX, "合計火力リスト"), 4)%
					LOCALS += FuncTagSetText_ShapeSize(LOCALS:1, WIDTH_ITEM:項目_雷撃, 1, 0)
				CASE 項目_ビーム
					LOCALS:1 = %LIST_GET(DIC_GET(艦船データ:SHIPLIST_IDX, "合計火力リスト"), 5)%
					LOCALS += FuncTagSetText_ShapeSize(LOCALS:1, WIDTH_ITEM:項目_ビーム, 1, 0)
				CASE 項目_対空
					LOCALS += FuncTagSetText_ShapeSize(DIC_GET(艦船データ:SHIPLIST_IDX, "対空"), WIDTH_ITEM:項目_対空, 1, 0)
				CASE 項目_回避
					LOCALS += FuncTagSetText_ShapeSize(DIC_GET(艦船データ:SHIPLIST_IDX, "回避"), WIDTH_ITEM:項目_回避, 1, 0)
				CASEELSE
			ENDSELECT
		NEXT
		CALL WndMgr_TextSet(WINDOW_ID, L_CNT++, @"%LOCALS%")
	NEXT
	;ページャー出力
	;-------------------------------------------------
	L_CNT = WINDOW_HEIGHT - 7
	VARSET LOCALS
	IF PAGE > 0
		LOCALS += @"@B:w@<<@/B@"
	ELSE
		LOCALS += @"@C:{カラー_選択不可}@<<@/C@"
	ENDIF
	LOCALS += @"@C:{カラー_注釈}@ {PAGE + 1} / {MAX_PAGE + 1} @/C@" 
	IF MAX_PAGE > 0
		LOCALS += @"@B:s@>>@/B@"
	ELSE
		LOCALS += @"@C:{カラー_選択不可}@>>@/C@"
	ENDIF
	DEBUGPRINTFORML %LOCALS%
	LOCALS = %FuncTagSetText_ShapeSize(@"%LOCALS%", WINDOW_WIDTH - 2, 1, 2)%
	CALL WndMgr_TextSet(WINDOW_ID, L_CNT++, @"%LOCALS%")
	;フッター出力
	;-------------------------------------------------
	L_CNT = WINDOW_HEIGHT - 6
	CALL WndMgr_TextSet(WINDOW_ID, L_CNT++, @"@C:{カラー_選択不可}@―@/C@" * (WINDOW_WIDTH / 2))
	VARSET LOCALS
	LOCALS += @"@C:{カラー_注釈}@ﾌｨﾙﾀｰ:@/C@ "
	FOR LOCAL, 0, SHIP_CATEGORY_COUNT
		IF FILTER_LIST:LOCAL == 1
			LOCALS += FuncTagSetText_ShapeSize(@"@C:{カラー_注釈}@@B:{LOCAL + FILTER_BTN}@\[%TOHALF(SHIP_CATEGORY_NAME:LOCAL)%\]@/B@@/C@  ", 20, 0, 0)
		ELSE
			LOCALS += FuncTagSetText_ShapeSize(@"@C:{カラー_選択不可}@@B:{LOCAL + FILTER_BTN}@\[%TOHALF(SHIP_CATEGORY_NAME:LOCAL)%\]@/B@@/C@  ", 20, 0, 0)
		ENDIF
	NEXT
	LOCALS += @"@C:{カラー_注釈}@[@B:{FILTER_BTN + SHIP_CATEGORY_COUNT + 1}@全ON@/B@/@B:{FILTER_BTN + SHIP_CATEGORY_COUNT + 2}@全OFF@/B@]@/C@"
	CALL WndMgr_TextSet(WINDOW_ID, L_CNT++, @"%LOCALS%")
	L_CNT++
	LOCALS '= @"@C:{カラー_選択不可}@[a][d]項目横スクロール　[w][s]ページ送り@/C@                                                        @B:q@[Q]閉じる@/B@"
	CALL WndMgr_TextSet(WINDOW_ID, L_CNT++, @"%LOCALS%")
	;画面描画
	;-------------------------------------------------
	CALL WndMgr_DisplayAll()
	REDRAW 2
;入力受付処理
;-------------------------------------------------
	ONEINPUTS
	SELECTCASE RESULTS
		CASE TOSTR(項目_艦名)
			INVERTBIT SORTMODE, 0
			CALL ARRAYSORT_DIC(艦船データ, "ID", SORTMODE)
		CASE TOSTR(項目_艦種)
			INVERTBIT SORTMODE, 0
			CALL ARRAYSORT_DIC(艦船データ, "艦種", SORTMODE)
		CASE TOSTR(項目_耐久)
			INVERTBIT SORTMODE, 0
			CALL ARRAYSORT_DIC(艦船データ, "最大HP", SORTMODE)
		CASE TOSTR(項目_シールド)
			INVERTBIT SORTMODE, 0
			CALL ARRAYSORT_DIC(艦船データ, "最大シールド", SORTMODE)
		CASE TOSTR(項目_座標)
			INVERTBIT SORTMODE, 0
			CALL ARRAYSORT_DIC(艦船データ, "座標", SORTMODE)
		CASE TOSTR(項目_任務)
			INVERTBIT SORTMODE, 0
			CALL ARRAYSORT_DIC(艦船データ, "任務", SORTMODE)
		CASE TOSTR(項目_実体弾)
			INVERTBIT SORTMODE, 0
			CALL ARRAYSORT_DIC(艦船データ, "実体弾", SORTMODE)
		CASE TOSTR(項目_レーザー)
			INVERTBIT SORTMODE, 0
			CALL ARRAYSORT_DIC(艦船データ, "レーザー", SORTMODE)
		CASE TOSTR(項目_ミサイル)
			INVERTBIT SORTMODE, 0
			CALL ARRAYSORT_DIC(艦船データ, "ミサイル", SORTMODE)
		CASE TOSTR(項目_航空)
			INVERTBIT SORTMODE, 0
			CALL ARRAYSORT_DIC(艦船データ, "航空", SORTMODE)
		CASE TOSTR(項目_雷撃)
			INVERTBIT SORTMODE, 0
			CALL ARRAYSORT_DIC(艦船データ, "雷撃", SORTMODE)
		CASE TOSTR(項目_ビーム)
			INVERTBIT SORTMODE, 0
			CALL ARRAYSORT_DIC(艦船データ, "ビーム", SORTMODE)
		CASE TOSTR(項目_対空)
			INVERTBIT SORTMODE, 0
			CALL ARRAYSORT_DIC(艦船データ, "対空", SORTMODE)
		CASE TOSTR(項目_回避)
			INVERTBIT SORTMODE, 0
			CALL ARRAYSORT_DIC(艦船データ, "回避", SORTMODE)
		CASE "a","A"
			SCROLL_IDX_COL --
			SIF SCROLL_IDX_COL < 0
				SCROLL_IDX_COL = 0
		CASE "d","D"
			SCROLL_IDX_COL ++
			SIF SCROLL_IDX_COL > VARSIZE("HEADER_ITEM") - NUM_PRINT_ITEM
				SCROLL_IDX_COL = VARSIZE("HEADER_ITEM") - NUM_PRINT_ITEM
		CASE "w","W"
			IF PAGE > 0
				PAGE --
			ENDIF
		CASE "s","S"
			IF PAGE < MAX_PAGE
				PAGE ++
			ENDIF
		CASE "q","Q"
		;艦船一覧ウィンドウを破棄
			CALL WndMgr_DestroyWindow(WINDOW_ID)
			CLEARLINE WINDOW_HEIGHT + 3
			BREAK
	ENDSELECT
	LOCAL = TOINT(RESULTS)
	SELECTCASE LOCAL
		CASE FILTER_BTN TO FILTER_BTN +  SHIP_CATEGORY_COUNT
			INVERTBIT FILTER_LIST:(LOCAL - FILTER_BTN), 0
			PAGE = 0
		;全てON
		CASE FILTER_BTN + SHIP_CATEGORY_COUNT + 1
			VARSET FILTER_LIST, 1
			PAGE = 0
		;全てOFF
		CASE FILTER_BTN + SHIP_CATEGORY_COUNT + 2
			VARSET FILTER_LIST, 0
			PAGE = 0
	ENDSELECT
	;;マウス入力によるスクロールや操作も実装したいが現仕様ではボタン入力と共存できない？ようなので一旦保留
	;;ホイールでぐりぐりしたかった
	; INPUTMOUSEKEY 100
	; ;右クリックで閉じる
	; IF RESULT:0 == 1 && RESULT:1 == 0x200000
	; 	;他のウィンドウをアクティブに戻す
	; 	CALL WndMgr_Attribute(-1, "Disabled", 0)
	; 	;艦船一覧ウィンドウを破棄
	; 	CALL WndMgr_DestroyWindow(WINDOW_ID)
	; 	CLEARLINE WINDOW_HEIGHT + 2
	; 	BREAK
	; ;ホイールスクロール
	; ELSEIF RESULT:0 == 2
	; 	IF RESULT:1 > 0
	; 		SCROLL_IDX_COL --
	; 		SIF SCROLL_IDX_COL < 0
	; 			SCROLL_IDX_COL = 0
	; 	ELSE
	; 		SCROLL_IDX_COL ++
	; 		SIF SCROLL_IDX_COL > VARSIZE("HEADER_ITEM") - NUM_PRINT_ITEM
	; 			SCROLL_IDX_COL = VARSIZE("HEADER_ITEM") - NUM_PRINT_ITEM
	; 	ENDIF
	; ELSEIF RESULT:0 > 4
	; 	PRINTFORMW {RESULT:0}
	; 	FORCEWAIT
	; ENDIF
WEND

RETURN