;Ａ感覚の可否
@ISABLUP5
;恋慕、親愛、淫乱、娼婦はMAXLV10
IF ABL:5 >= 10
	RETURN 0
ELSEIF ABL:5 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:淫乱 == 0 && TALENT:娼婦 == 0
	RETURN 0
ENDIF

CALL DECIDE_ABLUP5

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF


;Ａ感覚
@ABLUP5
DRAWLINE
;恋慕、親愛、淫乱、娼婦はMAXLV10
IF ABL:5 >= 10
	PRINTW 既にMAXです
	RETURN 0
ELSEIF ABL:5 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:淫乱 == 0 && TALENT:娼婦 == 0
	PRINTW 既にMAXです
	RETURN 0
ENDIF

CALL DECIDE_ABLUP5

PRINT [0] - 
PRINTS PALAMNAME:2
PRINT の珠×
PRINTV A
PRINT 、
PRINTS EXPNAME:1
PRINTV B
PRINT 以上
PRINT ……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
ENDIF
PRINTL 

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:5 += 1

IF RESULT == 0
	JUEL:2 -= A
ENDIF

PRINTS ABLNAME:5
PRINT のレベルが
PRINTV ABL:5
PRINTW になりました


;Ａ感覚LvUP判定
@DECIDE_ABLUP5
;条件別にＯＫかダメかを記録する
I = 0
;能力アップ可能なら1にする
Z = 0

IF ABL:5 == 0
	A = 1
	B = 2
ELSEIF ABL:5 == 1
	A = 50
	B = 10
	SIF EXP:Ａ経験 >= EXPLV:3
		A = 20
ELSEIF ABL:5 == 2
	A = 400
	B = 30
	SIF EXP:Ａ経験 >= EXPLV:4
		A = 100
ELSEIF ABL:5 == 3
	A = 5000
	B = 75
	SIF EXP:Ａ経験 >= EXPLV:5
		A = 4000
ELSEIF ABL:5 == 4
	A = 15000
	B = 150
	SIF EXP:Ａ経験 >= EXPLV:5
		A = 8000
ELSEIF ABL:5 == 5
	A = 30000
	B = 230
	SIF EXP:Ａ経験 >= EXPLV:5
		A = 24000
ELSEIF ABL:5 == 6
	A = 40000
	B = 270
ELSEIF ABL:5 == 7
	A = 50000
	B = 360
ELSEIF ABL:5 == 8
	A = 60000
	B = 420
ELSEIF ABL:5 == 9
	A = 70000
	B = 500
ENDIF

;淫乱
IF TALENT:淫乱
	TIMES A , 0.80
	TIMES B , 0.80
ENDIF
;娼婦
IF TALENT:娼婦
	TIMES A , 0.50
	TIMES B , 0.50
ENDIF

;0になった場合は1にする
SIF A == 0
	A = 1

;快Ａの珠で上げる
SIF JUEL:2 < A
	I |= 1

SIF EXP:Ａ経験 < B
	I |= 2

IF I == 0
	Z = 1
ENDIF

RETURN 1

