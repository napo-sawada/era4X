;-------------------------------------------------
;「奴隷商」の名称
;-------------------------------------------------
@SHOP_LIFE_NAME46
RESULTS:0 '= "奴隷商"
;'
;-------------------------------------------------
;「奴隷商」の選択可否
;-------------------------------------------------
@SHOP_LIFE_CHECK46
RETURN 1

;-------------------------------------------------
;「奴隷商」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY46
FLAG:拠点フェイズページ = 1
FLAG:夜這い = 0
MENUVAR:1 = 0
MENUVAR:2 = 0
MENUVAR:3 = 0
RETURN 0

;-------------------------------------------------
;「奴隷商」の右カラムボタン入力値をキャラ番号に変換する処理のオフ判定
;-------------------------------------------------
@SHOP_LIFE_USERSHOP_COLUMN_RIGHT_CONVERT_CHARANUM_OFF_CHECK46

;-------------------------------------------------
;「奴隷商」の右カラム表示処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW46
#DIM ID

;タイトル
CALL COLUMN_RIGHT_TITLE("募集要項の設定", "", "", "全", "0")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

PRINTPLAIN 奴隷商「ようこそいらっしゃいませ」
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

PRINTBUTTON "[この条件で探す]", 0
PRINTPLAIN   
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

CALL COLOR_PRINT(" 才能:", カラー_注釈)
CALL PRINT_SELECT_BUTTON(" [多能]", 110, MENUVAR:1 == 0)
CALL PRINT_SELECT_BUTTON(" [武官]", 111, MENUVAR:1 == 1)
CALL PRINT_SELECT_BUTTON(" [軍師]", 112, MENUVAR:1 == 2)
CALL PRINT_SELECT_BUTTON(" [政務]", 113, MENUVAR:1 == 3)
CALL PRINT_SELECT_BUTTON(" [芸能]", 114, MENUVAR:1 == 4)
CALL PRINT_SELECT_BUTTON(" [料理]", 115, MENUVAR:1 == 5)
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_PRINTL

CALL COLOR_PRINT(" 性別:", カラー_注釈)
CALL PRINT_SELECT_BUTTON(" [不問]", 120, MENUVAR:2 == 0)
CALL PRINT_SELECT_BUTTON(" [男性]", 121, MENUVAR:2 == 1)
CALL PRINT_SELECT_BUTTON(" [女性]", 122, MENUVAR:2 == 2)
CALL PRINT_SELECT_BUTTON(" [女双]", 123, MENUVAR:2 == 3)
CALL PRINT_SELECT_BUTTON(" [男双]", 124, MENUVAR:2 == 4)
CALL PRINT_SELECT_BUTTON(" [男娘]", 125, MENUVAR:2 == 5)
CALL PRINT_SELECT_BUTTON(" [男娘双]", 126, MENUVAR:2 == 6)
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_PRINTL

CALL COLOR_PRINT(" 名前:", カラー_注釈)
CALL PRINT_SELECT_BUTTON(" [和名]", 130, MENUVAR:3 == 0)
CALL PRINT_SELECT_BUTTON(" [洋風]", 131, MENUVAR:3 == 1)
CALL PRINT_SELECT_BUTTON(" [中華]", 132, MENUVAR:3 == 2)
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_PRINTL

CALL COLOR_PRINT(" 能力:", カラー_注釈)
CALL SELECTABLE_TALAENT_LIST

;残りは空行
CALL COLUMN_RIGHT_CLEAR

;-------------------------------------------------
;「奴隷商」の右カラムボタンの入力処理補佐
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SUB46(ARG:0)
#DIM INPUT_RESULT
;この条件で募集以外の数字なら戻る
SIF ARG:0 != 0
	RETURN 0

MENUVAR:11 = -1
MENUVAR:12 = 0

LOCAL:0 = GET_COMMANDER_NUM(CFLAG:MASTER:所属)

;君主が[動物耳]or[尻尾]を持つならケモ率高め
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
IF LOCAL:4 >= 0 && (TALENT:(LOCAL:4):動物耳 || TALENT:(LOCAL:4):しっぽ)
	LOCAL:5 = 75
ELSE
	LOCAL:5 = 1
ENDIF

$INPUT_LOOP_NEWCHARA

;指定の条件でランダムキャラを作成
CALL CREATE_RANDOM_CHARA(LOCAL:5, MENUVAR:1, MENUVAR:2 - 1, MENUVAR:3)
LOCAL:2 = RESULT

;士官の作成に失敗した場合
IF LOCAL:2 < 0
	PRINTW 残念ながら目ぼしい士官は見つかりませんでした…

;成功
ELSE
	CALL SINGLE_DRAWLINE
	PRINTFORML 「この奴隷などいかがでしょう？」

	$INPUT_LOOP_SELECT

	;SHOW_INFOの初期画面を基本情報にする
	FLAG:能力表示モード = 0

	;キャラの情報を表示
	CALL SINGLE_DRAWLINE
	CALL SHOW_INFO(LOCAL:2)
	CALL SINGLE_DRAWLINE

	PRINTBUTTON "[この奴隷を買う]", 0
	PRINT    
	PRINTBUTTON "[別の奴隷を探す]", 1
	PRINT    
	IF MENUVAR:11 == -1
	ELSEIF MENUVAR:12
		PRINTBUTTON "[進む]", 2
	ELSE
		PRINTBUTTON "[戻る]", 2
	ENDIF
	
	PRINT    
	PRINTBUTTON "[中止]", 9
	PRINTL 
	PRINTFORML 購入費用: %NUM_FORMAT(1000 + GET_FILTER_COST())%

	$INPUT_LOOP_TREAT
	INPUT
	INPUT_RESULT = RESULT
	

	IF INPUT_RESULT == 0 && (1000 + GET_FILTER_COST()) < MONEY
		PRINTL 奴隷の名前を変更しますか？
		CALL ASK_YN
		SIF RESULT == 0
			CALL CHANGE_NAME(LOCAL:2)
		
		CALL CHANGE_COUNTRY(LOCAL:2, CFLAG:MASTER:所属)
		PRINTFORMW %ANAME(LOCAL:2)%を購入しました
		IF MENUVAR:11 != -1
			CALL DELETE_CHARA(MENUVAR:11)
		ENDIF
		;このターンにおける登用済みフラグをON
		APPOINT_DONE = 1
		MONEY -= (1000 + GET_FILTER_COST())
		SHOP_TIME ++
		BEGIN TURNEND
		RETURN 1
	ELSEIF INPUT_RESULT == 0
		PRINTW 所持金が足りません
		;作成したキャラを削除
		CALL DELETE_CHARA(CHARANUM - 1)
		IF MENUVAR:11 != -1
			CALL DELETE_CHARA(CHARANUM - 1)
		ENDIF
	ELSEIF INPUT_RESULT == 1
		IF MENUVAR:11 != -1
			;いらないキャラを削除
			CALL DELETE_CHARA(MENUVAR:11)
		ENDIF
		MENUVAR:11 = CHARANUM - 1
		MENUVAR:12 = 0
		GOTO INPUT_LOOP_NEWCHARA
	ELSEIF INPUT_RESULT == 2
		IF MENUVAR:12
			MENUVAR:12 = 0
		ELSE
			MENUVAR:12 = 1
		ENDIF
			;スワップ
			LOCAL:21 = MENUVAR:11
			MENUVAR:11    = LOCAL:2
			LOCAL:2 = LOCAL:21
		GOTO INPUT_LOOP_SELECT
	ELSEIF INPUT_RESULT == 9
		;PRINTW 今回は購入を見送ることにしました
		;作成したキャラを削除
		CALL DELETE_CHARA(CHARANUM - 1)
		IF MENUVAR:11 != -1
			CALL DELETE_CHARA(CHARANUM - 1)
		ENDIF
	ELSE
		GOTO INPUT_LOOP_TREAT
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------
;「奴隷商」のリスト１入力処理
;　キャラ番号への変換オフのためARG:0=入力値
;-------------------------------------------------
@SHOP_LIFE_USERSHOP46(ARG:0)
IF ARG:0 >= 110 && ARG:0 <= 115
	MENUVAR:1 = ARG:0 - 110
ELSEIF ARG:0 >= 120 && ARG:0 <= 126
	MENUVAR:2 = ARG:0 - 120
ELSEIF ARG:0 >= 130 && ARG:0 <= 132
	MENUVAR:3 = ARG:0 - 130
ELSEIF ARG:0 >= 1000 && ARG:0 <= 1300
	;新たに素質を追加する場合、相反する素質は打ち消す
	IF TALENT_FILTER:(ARG:0 - 1000) == 0
		SELECTCASE ARG:0 - 1000
			CASE 11
				TALENT_FILTER:13 = 0
			CASE 13
				TALENT_FILTER:11 = 0
			CASE 15
				TALENT_FILTER:17 = 0
			CASE 17
				TALENT_FILTER:15 = 0
			CASE 22
				TALENT_FILTER:23 = 0
			CASE 23
				TALENT_FILTER:22 = 0
			CASE 25
				TALENT_FILTER:26 = 0
			CASE 26
				TALENT_FILTER:25 = 0
			CASE 27
				TALENT_FILTER:28 = 0
			CASE 28
				TALENT_FILTER:27 = 0
			CASE 30
				TALENT_FILTER:31 = 0
			CASE 31
				TALENT_FILTER:30 = 0
			CASE 32
				TALENT_FILTER:33 = 0
			CASE 33
				TALENT_FILTER:32 = 0
			CASE 35
				TALENT_FILTER:36 = 0
			CASE 36
				TALENT_FILTER:35 = 0
			CASE 40
				TALENT_FILTER:41 = 0
			CASE 41
				TALENT_FILTER:40 = 0
			CASE 42
				TALENT_FILTER:43 = 0
			CASE 43
				TALENT_FILTER:42 = 0
			CASE 50
				TALENT_FILTER:51 = 0
			CASE 51
				TALENT_FILTER:50 = 0
			CASE 61
				TALENT_FILTER:62 = 0
			CASE 62
				TALENT_FILTER:61 = 0
			CASE 70
				TALENT_FILTER:71 = 0
			CASE 71
				TALENT_FILTER:70 = 0
			CASE 81
				TALENT_FILTER:82 = 0
				TALENT_FILTER:83 = 0
			CASE 82, 83
				TALENT_FILTER:80 = 0
			CASE 101
				TALENT_FILTER:102 = 0
			CASE 102
				TALENT_FILTER:101 = 0
			CASE 103
				TALENT_FILTER:104 = 0
			CASE 104
				TALENT_FILTER:103 = 0
			CASE 105
				TALENT_FILTER:106 = 0
			CASE 106
				TALENT_FILTER:105 = 0
			CASE 107
				TALENT_FILTER:108 = 0
			CASE 108
				TALENT_FILTER:107 = 0
			CASE 109
				TALENT_FILTER:110 = 0
			CASE 110
				TALENT_FILTER:109 = 0
			CASE 111
				TALENT_FILTER:112 = 0
			CASE 112
				TALENT_FILTER:111 = 0
			CASE 115
				TALENT_FILTER:116 = 0
			CASE 116
				TALENT_FILTER:115 = 0
		ENDSELECT
	ENDIF
	TALENT_FILTER:(ARG:0 - 1000) = TALENT_FILTER:(ARG:0 - 1000) ? 0 # 1
ENDIF

RETURN 0


;-------------------------------------------------
;つけ外し可能なTALENTリスト
;-------------------------------------------------
@SELECTABLE_TALAENT_LIST
#DIM ID
#DIM COUNTER

COUNTER = 0

VARSIZE TALENT_FILTER
FOR ID, 0, RESULT:0
	SIF TALENTNAME:ID == ""
		CONTINUE
	SIF ID == 0
		CONTINUE
	SIF ID >= 150
		CONTINUE
	COUNTER ++
	LOCALS = [%TALENTNAME:ID%]
	LOCALS = %LOCALS, 16, LEFT%
	CALL PRINT_SELECT_BUTTON(LOCALS, 1000 + ID, TALENT_FILTER:ID, 1)
	IF COUNTER % 5 == 0
		CALL COLUMN_RIGHT_PRINTL
		CALL COLOR_PRINT(" 　　 ", カラー_注釈)
	ENDIF
NEXT

;-------------------------------------------------
;名前の変更
;-------------------------------------------------
@CHANGE_NAME(ID)
#DIM ID

PRINTFORML %CALLNAME:ID%の新しい名前を入力してください
$INPUT_LOOP1
INPUTS
CLEARLINE 1
PRINTFORML %RESULTS%でいいですか？
CALL ASK_YN
IF RESULT == 1
	GOTO INPUT_LOOP1
	CLEARLINE 2
ENDIF
CSTR:ID:0 = 
CSTR:ID:1 = 
CSTR:ID:5 =
CSTR:ID:6 = 

CSTR:ID:98 = %RESULTS%
NAME:ID = %RESULTS%

PRINTFORML %CALLNAME:ID%の新しい呼び名を入力してください
$INPUT_LOOP2
INPUTS
CLEARLINE 1
PRINTFORML %RESULTS%でいいですか？
CALL ASK_YN
IF RESULT == 1
	GOTO INPUT_LOOP2
	CLEARLINE 2
ENDIF
CALLNAME:ID = %RESULTS%

