;-------------------------------------------------
; SLG SHOP画面
;-------------------------------------------------
@SLG_SHOP
#DIM SELECTED = -1				;;クリックした座標データ
#DIM ITER
#DIM CURRENT_STAR_PORTID
#DIM CURRENT_PORTID
#DIM SECTOR_ID
#DIM X_POS
#DIM Y_POS
#DIM PREV_CHARACTER_SECTOR_ID
#DIM STATUS, STATUS_LENGTH
#DIMS strInputResult
#DIM Wnd4_LINES					;;window4展開用　桁数
#DIM PREV_CHARACTER_POSID

CALL ID2POS(CHARACTER_POSITION)
SECTOR_ID = RESULT:0
PREV_CHARACTER_SECTOR_ID = SECTOR_ID
PREV_CHARACTER_POSID = CHARACTER_POSITION
$INPUT_LOOP

IF PREV_CHARACTER_POSID != CHARACTER_POSITION
	PREV_CHARACTER_POSID = CHARACTER_POSITION
	SELECTED = -1	;移動したなら選択マスをリセット
	CALL ID2POS(CHARACTER_POSITION)
	IF RESULT != PREV_CHARACTER_SECTOR_ID
		PREV_CHARACTER_SECTOR_ID = RESULT
		SECTOR_ID = RESULT
	ENDIF
ENDIF

;CALL DRAW_MAP(SECTOR_ID)
CALL DRAW_WINDOW(SECTOR_ID, SELECTED, BOARDING_SHIP_ID , 1)
;;;window4 展開用に数値保持
Wnd4_LINES = RESULT

; 変数初期化
CURRENT_STAR_PORTID = -1
CURRENT_PORTID = -1
;状態表示とCONTROL表示
IF BOARDING_SHIP_ID != -1 && SHIP_MISSION:BOARDING_SHIP_ID == 待機
	PRINTFORML 現在%SHIP_NAME:BOARDING_SHIP_ID%に搭乗しています
	TRYCALLFORM GET_ENGIN_STATUS_{SHIP_EQUIP:エンジン:0:BOARDING_SHIP_ID}(STATUS)
	IF STATUS:SPEED == 1
		PRINTBUTTON "   [w]↑", "w"
		PRINTL 
		PRINTBUTTON "[a]←", "a"
		PRINT     
		PRINTBUTTON "→[d]", "d"
		PRINTL 
		PRINTBUTTON "   [s]↓", "s"
		PRINTL 
	ELSEIF STATUS:SPEED == 2
		PRINTBUTTON "     [ww]↑", "ww"
		PRINTBUTTON "[w]↑↑", "w"
		PRINTL 
		PRINTBUTTON "[aa]←", "aa"
		PRINTBUTTON "[a]←←", "a"
		PRINT     
		PRINTBUTTON "→→[d]", "d"
		PRINTBUTTON "→[dd]", "dd"
		PRINTL 
		PRINTBUTTON "     [ss]↓", "ss"
		PRINTBUTTON "[s]↓↓", "s"
		PRINTL 
	ENDIF
ELSEIF BOARDING_SHIP_ID != -1 && SHIP_MISSION:BOARDING_SHIP_ID == 移動
	PRINTFORML 現在%SHIP_NAME:BOARDING_SHIP_ID%に搭乗しています
	PRINTFORML 現在自動航行中です
	PRINTBUTTON "[v] 時間を進める", "v"
	PRINT 　
	PRINTBUTTON "[c] 自動航行中止", "c"
	PRINTL
ELSEIF BOARDING_SHIP_ID != -1 && SHIP_MISSION:BOARDING_SHIP_ID == パトロール
	PRINTFORML 現在%SHIP_NAME:BOARDING_SHIP_ID%に搭乗しています
	PRINTFORML 現在パトロール中です
	PRINTBUTTON "[v] 時間を進める", "v"
	PRINT 　
	PRINTBUTTON "[c] パトロール中止", "c"
	PRINTL
ELSEIF BOARDING_SHIP_ID != -1 && SHIP_MISSION:BOARDING_SHIP_ID == 交易
	PRINTFORML 現在%SHIP_NAME:BOARDING_SHIP_ID%に搭乗しています
	PRINTFORML 現在交易中です
	PRINTBUTTON "[v] 時間を進める", "v"
	PRINT 　
	PRINTBUTTON "[c] 交易中止", "c"
	PRINTL
ELSEIF BOARDING_SHIP_ID != -1 && SHIP_MISSION:BOARDING_SHIP_ID == 採掘
	PRINTFORML 現在%SHIP_NAME:BOARDING_SHIP_ID%に搭乗しています
	PRINTFORML 現在採掘中です
	PRINTBUTTON "[v] 時間を進める", "v"
	PRINT 　
	PRINTBUTTON "[c] 採掘中止", "c"
	PRINTL
ELSEIF BOARDING_SHIP_ID != -1 && SHIP_MISSION:BOARDING_SHIP_ID == 追従
	PRINTFORML 現在%SHIP_NAME:(SHIP_FOLLOW:BOARDING_SHIP_ID)%を追従しています
	PRINTBUTTON "[v] 時間を進める", "v"
	PRINT 　
	PRINTBUTTON "[c] 追従中止", "c"
	PRINTL
ELSE
	FOR LOCAL, 0, 星系数
		IF 星系座標ID:LOCAL == CHARACTER_POSITION
			PRINTFORML 現在 星＃{LOCAL} (%COUNTRY_NAME:(STAR_OWNER:LOCAL)%)に滞在しています
			PRINTBUTTON "[v] 時間を進める", "v"
			CONTINUE
		ENDIF
	NEXT
ENDIF

PRINT 　　　
PRINTBUTTON "[q] 調教画面へ", "q"
PRINT 　

;PRINTBUTTON "[v] 時間を進める", "v"
;PRINT 　

;PRINTBUTTON "[z] いんふぉ", "z"
;PRINT 　

FOR ITER, 0, 星系数
	IF 星系座標ID:ITER == CHARACTER_POSITION
		PRINTBUTTON "[f] 宇宙港", "f"
		CURRENT_STAR_PORTID = ITER
		PRINT 　
	ENDIF
NEXT
FOR ITER, 0, MAX_PORT
	IF PORT_POSITION_ID:ITER == CHARACTER_POSITION
		PRINTBUTTON "[f] 宇宙港", "f"
		CURRENT_PORTID = ITER
		PRINT 　
	ENDIF
NEXT

; 資源回収
CALL IS_MINING(CHARACTER_POSITION)
IF RESULT == 1
	PRINTBUTTON "[r] 採掘", "r"
	PRINT 　
ENDIF

CALL ID2POS(CHARACTER_POSITION)

; アノマリー
STRLENFORM %ANOMALY_MAP:RESULT:(RESULT:1):(RESULT:2)%
IF RESULT != 0
	PRINTBUTTON "[e] アノマリーを調査", "e"
	PRINT　
ENDIF
;DRAWLINE

; 宇宙港建造
CALL IS_BUILDING(CHARACTER_POSITION)
IF RESULT == 1
	PRINTBUTTON "[t] 宇宙港建造", "t"
	PRINT 　
ENDIF
CALL IS_BUILDUP(CHARACTER_POSITION)
IF RESULT == 1
	PRINTBUTTON "[t] 宇宙港建造", "t"
	PRINT 　
ENDIF

PRINTL
DRAWLINE
;入力

$INPUT_LOOP2
INPUTS


strInputResult '= RESULTS
;;; "_"入力はwindow背景クリックした際の動作なのでほっとく
IF strInputResult == "_"
	CLEARLINE 1
	GOTO INPUT_LOOP2
ENDIF

CLEARLINE LINECOUNT

;;数字解析できた場合
;;セクター内の特定地点をクリックした場合、もしくはセクターマップ切り替え操作した場合のどちらか
ISNUMERIC strInputResult
IF RESULT:0
	TOINT strInputResult
	IF RESULT >= 0 && RESULT <= 999999
		SECTOR_ID = RESULT / 10000
		IF RESULT % 10000 == 9999	;;セクター切り替え操作
			SELECTED = RESULT		;; SECTOR_ID + 9999 のまま描画にもどる
			GOTO INPUT_LOOP
		ELSE						;;同セクター内で何かをクリック
			SELECTED = RESULT		;; SELECTEDに入力値を入れ描画にもどる	
			;CALL ID2POS(SELECTED)
			;X_POS = LIMIT(RESULT:1, 0, MAP_WIDTH - 1)
			;Y_POS = LIMIT(RESULT:2, 0, MAP_HEIGHT - 1)
			GOTO INPUT_LOOP
		ENDIF
	ENDIF
ENDIF

;;;　以下、INPUTSが数字でない。文字列照合

SELECTCASE strInputResult
	; 調教画面へ戻る
	CASE "q"
		RETURN
	; 資源回収
	CASE "r"
		CALL MINING(CHARACTER_POSITION, PLAYER_COUNTRY)
		CALL SLG_TURN_END(1)
		GOTO INPUT_LOOP
	; アノマリー
	CASE "e"
		CALL ID2POS(SHIP_POSITION:BOARDING_SHIP_ID)
		SECTOR_ID = RESULT:0
		X_POS = RESULT:1
		Y_POS = RESULT:2
		STRLENFORM %ANOMALY_MAP:SECTOR_ID:X_POS:Y_POS%
		IF RESULT != 0
			TRYCALLFORM ANOMALY_BODY_%ANOMALY_MAP:SECTOR_ID:X_POS:Y_POS%
		ENDIF
		GOTO INPUT_LOOP
	; 宇宙港建造
	CASE "t"
		CALL IS_BUILDING(CHARACTER_POSITION)
		IF RESULT == 1
			CALL CREATE_STAR_PORT(CHARACTER_POSITION, PLAYER_COUNTRY)
			SIF RESULT == 1
				CALL SLG_TURN_END(1)
		ENDIF
		CALL IS_BUILDUP(CHARACTER_POSITION)
		IF RESULT == 1
			CALL BUILD_PORT(CHARACTER_POSITION, PLAYER_COUNTRY)
			SIF RESULT == 1
				CALL SLG_TURN_END(1)
		ENDIF
		GOTO INPUT_LOOP
	; 宇宙港へ
	CASE "f"
		IF CURRENT_STAR_PORTID != -1
			CALL STAR_MENU(CURRENT_STAR_PORTID)
			SIF RESULT == 1
				RETURN
			GOTO INPUT_LOOP
		ELSEIF CURRENT_PORTID != -1 && PORT_COMPLETE:CURRENT_PORTID == 1
			CALL PORT_MENU(CURRENT_PORTID)
			SIF RESULT == 1
				RETURN
		ENDIF
		GOTO INPUT_LOOP
	;ターンを進める
	CASE "v"
		CALL SLG_TURN_END(1)
		GOTO INPUT_LOOP
	;タスクを中断する
	CASE "c"
		SIF BOARDING_SHIP_ID != -1 && SHIP_MISSION:BOARDING_SHIP_ID != 待機
			SHIP_MISSION:BOARDING_SHIP_ID = 待機
		GOTO INPUT_LOOP
	;window4メッセージ枠の拡張表示
	;基本4行＋枠２のタテ６マスのwindowとしている
	;[一覧表示 / 戻す]　の操作で必要行数まで拡張するための処理。それでも最大100行。これ以上は
	;;WindowDrawer.ERH の変更が必要。ここだけ増やすとおちる。
	CASE "L"
		CALL WndMgr_Exist( 4 )
		IF RESULT:0
			Wnd4_LINES += 2 + 1 							;;必要行数＋２（枠）＋１ [一覧表示 / 戻す]
			Wnd4_LINES = LIMIT ( Wnd4_LINES , 6, 102) 	;;window表示上限50→102にした　重すぎたら検討
			CALL WndMgr_GetRectangle( 4 )
			IF RESULT:3 == 6
				CALL WndMgr_Rectangle( 4, RESULT:0 ,RESULT:1 , RESULT:2 , Wnd4_LINES , RESULT:6 )
			ELSE
				CALL WndMgr_Rectangle( 4, RESULT:0 ,RESULT:1 , RESULT:2 , 6 , RESULT:6 )
			ENDIF
		ENDIF
		GOTO INPUT_LOOP
	CASEELSE
ENDSELECT


; 調教画面へ戻る
;IF strInputResult == "q"
;	RETURN
;ENDIF

; 資源回収
;IF RESULTS =="r"
;	CALL MINING(CHARACTER_POSITION, PLAYER_COUNTRY)
;	CALL SLG_TURN_END(1)
;	GOTO INPUT_LOOP
;ENDIF


;; アノマリー
;IF RESULTS == "e"
;	CALL ID2POS(SHIP_POSITION:BOARDING_SHIP_ID)
;	SECTOR_ID = RESULT:0
;	X_POS = RESULT:1
;	Y_POS = RESULT:2
;	STRLENFORM %ANOMALY_MAP:SECTOR_ID:X_POS:Y_POS%
;	IF RESULT != 0
;		TRYCALLFORM ANOMALY_BODY_%ANOMALY_MAP:SECTOR_ID:X_POS:Y_POS%
;	ENDIF
;	GOTO INPUT_LOOP
;ENDIF

; 宇宙港建造
;IF RESULTS =="t"
;	CALL IS_BUILDING(CHARACTER_POSITION)
;	IF RESULT == 1
;		CALL CREATE_STAR_PORT(CHARACTER_POSITION, PLAYER_COUNTRY)
;		SIF RESULT == 1
;			CALL SLG_TURN_END(1)
;	ENDIF
;	CALL IS_BUILDUP(CHARACTER_POSITION)
;	IF RESULT == 1
;		CALL BUILD_PORT(CHARACTER_POSITION, PLAYER_COUNTRY)
;		SIF RESULT == 1
;			CALL SLG_TURN_END(1)
;	ENDIF
;	GOTO INPUT_LOOP
;ENDIF

;IF BOARDING_SHIP_ID != -1 && (SHIP_MISSION:BOARDING_SHIP_ID == 移動 || SHIP_MISSION:BOARDING_SHIP_ID == パトロール || SHIP_MISSION:BOARDING_SHIP_ID == 交易 || SHIP_MISSION:BOARDING_SHIP_ID == 採掘 || SHIP_MISSION:BOARDING_SHIP_ID == 追従)
;	IF RESULTS == "v"
;		CALL SLG_TURN_END(1)
;			GOTO INPUT_LOOP
;	ELSEIF RESULTS == "c"
;		SHIP_MISSION:BOARDING_SHIP_ID = 待機
;			GOTO INPUT_LOOP
;	ENDIF

;ENDIF

; 船による移動
IF BOARDING_SHIP_ID != -1 && (RESULTS == "w" || RESULTS == "d" || RESULTS == "a" || RESULTS == "s" || RESULTS == "ww" || RESULTS == "dd" || RESULTS == "aa" || RESULTS == "ss") && SHIP_MISSION:BOARDING_SHIP_ID == 待機
	TRYCALLFORM GET_ENGIN_STATUS_{SHIP_EQUIP:エンジン:0:BOARDING_SHIP_ID}(STATUS)
	IF STATUS:SPEED == 2
		SELECTCASE RESULTS
			CASE "w"
				CALL SHIP_MOVE_FOR(8, BOARDING_SHIP_ID)
			CASE "d"
				CALL SHIP_MOVE_FOR(6, BOARDING_SHIP_ID)
			CASE "a"
				CALL SHIP_MOVE_FOR(4, BOARDING_SHIP_ID)
			CASE "s"
				CALL SHIP_MOVE_FOR(2, BOARDING_SHIP_ID)
			CASE "ww"
				CALL SHIP_MOVE_FOR(8, BOARDING_SHIP_ID, 1)
			CASE "dd"
				CALL SHIP_MOVE_FOR(6, BOARDING_SHIP_ID, 1)
			CASE "aa"
				CALL SHIP_MOVE_FOR(4, BOARDING_SHIP_ID, 1)
			CASE "ss"
				CALL SHIP_MOVE_FOR(2, BOARDING_SHIP_ID, 1)
		ENDSELECT
	ELSE
		SELECTCASE RESULTS
			CASE "w"
				CALL SHIP_MOVE_FOR(8, BOARDING_SHIP_ID)
			CASE "d"
				CALL SHIP_MOVE_FOR(6, BOARDING_SHIP_ID)
			CASE "a"
				CALL SHIP_MOVE_FOR(4, BOARDING_SHIP_ID)
			CASE "s"
				CALL SHIP_MOVE_FOR(2, BOARDING_SHIP_ID)
		ENDSELECT
	ENDIF
	CALL ID2POS(SHIP_POSITION:BOARDING_SHIP_ID)
	X_POS = RESULT:1
	Y_POS = RESULT:2
	CALL SLG_TURN_END(1)
	GOTO INPUT_LOOP
ENDIF

;; 宇宙港へ
;IF RESULTS == "f" && CURRENT_STAR_PORTID != -1
;	CALL STAR_MENU(CURRENT_STAR_PORTID)
;	SIF RESULT == 1
;		RETURN
;	GOTO INPUT_LOOP
;ELSEIF RESULTS == "f" && CURRENT_PORTID != -1 && PORT_COMPLETE:CURRENT_PORTID == 1
;	CALL PORT_MENU(CURRENT_PORTID)
;	SIF RESULT == 1
;		RETURN
;	GOTO INPUT_LOOP
;ENDIF


; マップ選択
;SELECTCASE GET_MAP(SELECTED)
;	CASE 無
;		PRINTFORML 未探索地域
;	CASE 明瞭
;		PRINTFORML 空白宙域
;		CALL SHOW_POSITION_INFO(SELECTED)
;	CASE 星
;		CALL MAPID2PORTID(SELECTED)
;		PRINTFORML 中立星(所属: %COUNTRY_NAME:(STAR_OWNER:RESULT)%)
;		;今いる星じゃなかったら返す
;		CALL POS2ID(SECTOR_ID, X_POS, Y_POS)
;		IF RESULT != CHARACTER_POSITION
;			GOTO INPUT_LOOP
;		ENDIF
;		CALL MAPID2PORTID(SELECTED)
;		CALL STAR_MENU(RESULT)
;		SIF RESULT == 1
;			RETURN
;	CASE 宇宙港
;		PRINTFORML 宇宙港
;		CALL SHOW_POSITION_INFO(SELECTED)
;	CASE 資源
;		PRINTFORML ◆資源採掘可能エリア
;		CALL SHOW_POSITION_INFO(SELECTED)
;	CASE ANOMALY
;		PRINTFORML ◆アノマリー
;		CALL SHOW_POSITION_INFO(SELECTED)
;	CASE ワープゲート
;		PRINTFORML ◆ワープゲート
;		CALL SHOW_POSITION_INFO(SELECTED)
;ENDSELECT
;
GOTO INPUT_LOOP

;-------------------------------------------------
; 宙域詳細表示
;-------------------------------------------------
@SHOW_POSITION_INFO(POSID)
#DIM POSID
#DIM SECTOR_ID
#DIM X_POS
#DIM Y_POS

CALL ID2POS(POSID)
SECTOR_ID = RESULT:0
X_POS = RESULT:1
Y_POS = RESULT:2

CALL GET_SHIP_LIST_FROM_POS(POSID)
IF MAP:SECTOR_ID:X_POS:Y_POS == 資源
	PRINTFORM %GET_RESOURCE_NAME(RESOURCE_MAP:SECTOR_ID:X_POS:Y_POS)%
	PRINT : 
	PRINTFORML %GET_RESOURCE_DESCRIPTION(RESOURCE_MAP:SECTOR_ID:X_POS:Y_POS)%
ENDIF

IF MAP:SECTOR_ID:X_POS:Y_POS == ワープゲート
	SIF X_POS == 0 && Y_POS == MAP_HEIGHT / 2
		PRINTFORML セクター{SECTOR_ID - 1}へ通じるゲート
	SIF X_POS == MAP_WIDTH - 1 && Y_POS == MAP_HEIGHT / 2
		PRINTFORML セクター{SECTOR_ID + 1}へ通じるゲート
	SIF X_POS == MAP_WIDTH / 2 && Y_POS == 0
		PRINTFORML セクター{SECTOR_ID - 10}へ通じるゲート
	SIF X_POS == MAP_WIDTH / 2 && Y_POS == MAP_HEIGHT - 1
		PRINTFORML セクター{SECTOR_ID + 10} へ通じるゲート
ENDIF

SIF SHIP_LIST_LENGTH > 0
	PRINTL ◆宙域艦隊
FOR LOCAL, 0, SHIP_LIST_LENGTH
	PRINTFORM %COUNTRY_NAME:(SHIP_COUNTRY:(SHIP_LIST:LOCAL))%所属 %SHIP_NAME:(SHIP_LIST:LOCAL)%
	IF SHIP_MISSION:(SHIP_LIST:LOCAL) == パトロール
		PRINTL (パトロール任務中)
	ELSEIF SHIP_MISSION:(SHIP_LIST:LOCAL) == 採掘
		PRINTL (採掘中)
	ELSE
		PRINTL
	ENDIF
NEXT

;-------------------------------------------------
; 星メニュー
;-------------------------------------------------
@STAR_MENU(PORTID)
#DIM PORTID
#DIMS LEFT_COLUMNS, 50
#DIM LEFT_COLUMNS_BUTTON_VALUE, 50
#DIM CURRENT_MODE
#DIM LINE_ITER


VARSET LEFT_COLUMNS, " "
VARSET LEFT_COLUMNS_BUTTON_VALUE, NONE_LINE
CURRENT_MODE = 0

$INPUT_LOOP
LINE_ITER = 0
; ^q^
LEFT_COLUMNS:LINE_ITER = %"　宇宙港", LEFT_COLUMN_LENGTH, LEFT%
LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = WHITE_LINE
LINE_ITER ++
LEFT_COLUMNS:LINE_ITER = %"---------------------", LEFT_COLUMN_LENGTH, LEFT%
LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = GRAY_LINE
LINE_ITER ++
LEFT_COLUMNS:LINE_ITER = %"　[0] 宇宙港", LEFT_COLUMN_LENGTH, LEFT%
IF CURRENT_MODE == 0
	LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = GREEN_LINE
ELSE
	LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = 0
ENDIF
LINE_ITER ++
LEFT_COLUMNS:LINE_ITER = %"　[1] 艦船購入", LEFT_COLUMN_LENGTH, LEFT%
IF CURRENT_MODE == 1
	LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = GREEN_LINE
ELSE
	LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = 1
ENDIF
LINE_ITER ++
LEFT_COLUMNS:LINE_ITER = %"　[2] 装備更新", LEFT_COLUMN_LENGTH, LEFT%
IF CURRENT_MODE == 2
	LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = GREEN_LINE
ELSE
	LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = 2
ENDIF
LINE_ITER ++
LEFT_COLUMNS:LINE_ITER = %"　[3] タスクの割り振り", LEFT_COLUMN_LENGTH, LEFT%
IF CURRENT_MODE == 3
	LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = GREEN_LINE
ELSE
	LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = 3
ENDIF
LINE_ITER ++
LEFT_COLUMNS:LINE_ITER = %"　[4] クエスト", LEFT_COLUMN_LENGTH, LEFT%
IF CURRENT_MODE == 4
	LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = GREEN_LINE
ELSE
	LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = 4
ENDIF
LINE_ITER ++
IF HUMAN_LAB == CHARACTER_POSITION && QUEST_PROGRESS:0 >= 3
	LEFT_COLUMNS:LINE_ITER = %"　[5] 人体研究所", LEFT_COLUMN_LENGTH, LEFT%
	IF CURRENT_MODE == 5
		LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = GREEN_LINE
	ELSE
		LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = 5
	ENDIF
	LINE_ITER ++
ENDIF
LEFT_COLUMNS:LINE_ITER = %"　[6] 素材販売", LEFT_COLUMN_LENGTH, LEFT%
IF CURRENT_MODE == 6
	LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = GREEN_LINE
ELSE
	LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = 6
ENDIF
LINE_ITER ++
LEFT_COLUMNS:LINE_ITER = %"　[7] 加工工場", LEFT_COLUMN_LENGTH, LEFT%
IF CURRENT_MODE == 7
	LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = GREEN_LINE
ELSE
	LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = 7
ENDIF
LINE_ITER ++
LEFT_COLUMNS:LINE_ITER = %"　[9] インフォメーション", LEFT_COLUMN_LENGTH, LEFT%
IF CURRENT_MODE == 7
	LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = GREEN_LINE
ELSE
	LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = 9
ENDIF
LINE_ITER ++
LEFT_COLUMNS:LINE_ITER = %"　[10] 宙域画面へ", LEFT_COLUMN_LENGTH, LEFT%
LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = 10
LINE_ITER ++
LEFT_COLUMNS:LINE_ITER = %"　[11] 奴隷の調教", LEFT_COLUMN_LENGTH, LEFT%
LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = 11
LINE_ITER ++
LEFT_COLUMNS:LINE_ITER = %"　[12] インフォ", LEFT_COLUMN_LENGTH, LEFT%
LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER = 12


DRAWLINE
PRINTFORML 　星(%COUNTRY_NAME:(STAR_OWNER:PORTID)%統治 - %STAR_NAME:PORTID%)

IF CURRENT_MODE == 0
	CALL SPACE_PORT(PORTID, 0, LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE)
ELSEIF CURRENT_MODE == 1
	CALL SLG_SHIP_SHOP(星系座標ID:PORTID, STAR_OWNER:PORTID, LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE)
ELSEIF CURRENT_MODE == 2
	CALL SLG_EQUIP_SHOP(星系座標ID:PORTID, STAR_OWNER:PORTID, LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE)
ELSEIF CURRENT_MODE == 3
	CALL SETUP_TASK_LIST(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE)
ELSEIF CURRENT_MODE == 4
	CALL QUEST_LIST(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE)
ELSEIF CURRENT_MODE == 5
	CALL HUMAN_LAB
ELSEIF CURRENT_MODE == 6
	CALL SLG_ITEM_SHOP(PORTID, 0)
ELSEIF CURRENT_MODE == 7
	CALL FACTORY(PORTID, PLAYER_COUNTRY, 0)
ELSEIF CURRENT_MODE == 9
	CALL INFORMATION_INDEX
ENDIF

IF RESULT == 0
	CURRENT_MODE = 0
	GOTO INPUT_LOOP
ELSEIF RESULT == 1
	CURRENT_MODE = 1
	GOTO INPUT_LOOP
ELSEIF RESULT == 2
	CURRENT_MODE = 2
	GOTO INPUT_LOOP
ELSEIF RESULT == 3
	CURRENT_MODE = 3
	GOTO INPUT_LOOP
ELSEIF RESULT == 4
	CURRENT_MODE = 4
	GOTO INPUT_LOOP
ELSEIF RESULT == 5 && HUMAN_LAB == CHARACTER_POSITION && QUEST_PROGRESS:0 >= 3
	CURRENT_MODE = 5
	GOTO INPUT_LOOP
ELSEIF RESULT == 6
	CURRENT_MODE = 6
	GOTO INPUT_LOOP
ELSEIF RESULT == 7
	CURRENT_MODE = 7
	GOTO INPUT_LOOP
ELSEIF RESULT == 9
	CURRENT_MODE = 9
	GOTO INPUT_LOOP
ELSEIF RESULT == 10
	RETURN 0
ELSEIF RESULT == 11
	RETURN 1
ELSEIF RESULT == 12
	CALL SHOW_INFO_ASSET
ELSE
	GOTO INPUT_LOOP
ENDIF

;-------------------------------------------------
; 宇宙港
;-------------------------------------------------
@SPACE_PORT(PORTID, IS_NOT_STAR_PORT, LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE)
#DIM PORTID
#DIM IS_NOT_STAR_PORT
#DIMS REF LEFT_COLUMNS
#DIM REF LEFT_COLUMNS_BUTTON_VALUE
#DIM SELECTED
#DIMS LOCAL_SHIP_NAME
#DIM POSID
#DIM LINE_ITER
#DIM ITER
#DIM TOTAL_SHIP_COUNT
#DIM SHIP_COUNTER
#DIM CONST MAX_SHOW_LIST = 30
#DIM CURRENT_PAGE
#DIM MAX_PAGE
#DIM CURRENT_SHIP_LIST, MAX_SHIP
#DIM PAGE_START
#DIM PAGE_END
#DIM LEFT_COLUMN_SIZE
#DIM SHIP_STATUS, SHIP_STATUS_LENGTH

#DIM PREVIOUS_LINECOUNT

#DIM COLUMN_ORDER = データ_艦名, データ_艦種, データ_所属, データ_任務, データ_HP, データ_シールド, データ_実弾, データ_レーザー, データ_ミサイル, データ_戦闘機, データ_魚雷, データ_対空, データ_回避
#DIM DATA_WIDTH, 船舶一覧データ数 
#DIM INDEX
;列幅
DATA_WIDTH:データ_艦名 = 20
DATA_WIDTH:データ_艦種 = 10
DATA_WIDTH:データ_所属 = 16
DATA_WIDTH:データ_任務 = 4
DATA_WIDTH:データ_HP = 11
DATA_WIDTH:データ_シールド = 11
DATA_WIDTH:データ_実弾 = 4
DATA_WIDTH:データ_レーザー = 4
DATA_WIDTH:データ_ミサイル = 4
DATA_WIDTH:データ_戦闘機 = 4
DATA_WIDTH:データ_魚雷 = 4
DATA_WIDTH:データ_対空 = 4
DATA_WIDTH:データ_回避 = 4
INDEX = 1

TOTAL_SHIP_COUNT = 0
SHIP_COUNTER = 0
CURRENT_PAGE = 0
VARSET CURRENT_SHIP_LIST, -1

POSID = IS_NOT_STAR_PORT == 0 ? 星系座標ID:PORTID # PORT_POSITION_ID:PORTID

PREVIOUS_LINECOUNT = LINECOUNT
$INPUT_LOOP
FINDELEMENT LEFT_COLUMNS_BUTTON_VALUE, NONE_LINE
LEFT_COLUMN_SIZE = RESULT
LINE_ITER = 0
VARSET CURRENT_SHIP_LIST, -1

; UI
DRAWLINE
CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
PRINTFORML %"　停泊中の艦船", RIGHT_COLUMN_LENGTH, LEFT%
LINE_ITER ++

CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
CALL CREATE_HEADLINE_FOR_SHIP_LIST(INDEX, COLUMN_ORDER, DATA_WIDTH)
LINE_ITER ++

CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
CALL DRAW_RIGHT_COLUMN_GRAY_LINE
LINE_ITER ++
; UI以上

; CURRENT_SHIP_LISTに現在停泊中の船を入れる
FOR LOCAL, 0, MAX_SHIP
	IF SHIP_POSITION:LOCAL == POSID
		FINDELEMENT CURRENT_SHIP_LIST, -1
		CURRENT_SHIP_LIST:RESULT = LOCAL
	ENDIF
NEXT
CALL SORT_STAR_PORT_SHIPS(CURRENT_SHIP_LIST)

FINDELEMENT CURRENT_SHIP_LIST, -1
TOTAL_SHIP_COUNT = RESULT
PAGE_START = CURRENT_PAGE * MAX_SHOW_LIST
PAGE_END = PAGE_START + MAX_SHOW_LIST > TOTAL_SHIP_COUNT ? TOTAL_SHIP_COUNT # PAGE_START + MAX_SHOW_LIST
MAX_PAGE = TOTAL_SHIP_COUNT / MAX_SHOW_LIST

; 描画
FOR LOCAL, PAGE_START, PAGE_END
	CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)

	CALL CREATE_COLUMN_FOR_SHIP_LIST(CURRENT_SHIP_LIST:LOCAL, INDEX, COLUMN_ORDER, DATA_WIDTH)
	SIF SHIP_COUNTRY:(CURRENT_SHIP_LIST:LOCAL) == PLAYER_COUNTRY
		SETCOLOR 0X00FF00
	PRINTBUTTON @"　%RESULTS%", (CURRENT_SHIP_LIST:LOCAL) + 100
	RESETCOLOR
	PRINTL
	LINE_ITER ++
NEXT
;)
; 左カラムが右絡むより長かった場合の処理
FOR LOCAL, LINE_ITER, LEFT_COLUMN_SIZE
	CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
	PRINTL
	LINE_ITER ++
NEXT
PRINTFORM %"", LEFT_COLUMN_LENGTH, LEFT%
CALL DRAW_RIGHT_COLUMN_GRAY_LINE
; 描画以上
;-----------------------------------------------------
;)DISPLAY CONTROL
;-----------------------------------------------------
IF CURRENT_PAGE > 0 || CURRENT_PAGE < MAX_PAGE
	PRINTFORM %"", LEFT_COLUMN_LENGTH, LEFT%
	IF CURRENT_PAGE > 0
		LOCALS = %"[<]", 30, RIGHT%
		PRINTBUTTON @"%LOCALS%", -9999
	ELSE
		PRINTFORM %"", 30, RIGHT%
	ENDIF
	IF CURRENT_PAGE < MAX_PAGE
		PRINTBUTTON "[>]", 9999
	ENDIF
	PRINTL
	PRINTFORM %"", LEFT_COLUMN_LENGTH, LEFT%
	CALL DRAW_RIGHT_COLUMN_GRAY_LINE
ENDIF

INPUT
SELECTED = RESULT
PRINTFORML {LEFT_COLUMN_SIZE}
IF RESULT == 9999
	CURRENT_PAGE ++
	CLEARLINE (LINECOUNT - PREVIOUS_LINECOUNT)
	GOTO INPUT_LOOP
ELSEIF RESULT == -9999
	CURRENT_PAGE --
	CLEARLINE (LINECOUNT - PREVIOUS_LINECOUNT)
	GOTO INPUT_LOOP
ELSEIF RESULT <= LEFT_COLUMNS_BUTTON_VALUE:(LEFT_COLUMN_SIZE - 1)
	RETURN RESULT
ENDIF

IF RESULT >= 10000 && RESULT < 20000
	RESULT -= 10000
	;同じソートキーを選択された場合、ソート順を変更する
	IF RESULT == ソートキー
		IF 降順ソート == 1
			降順ソート = 0
		ELSE
			降順ソート = 1
		ENDIF
	ENDIF

	ソートキー = RESULT
	CLEARLINE (LINECOUNT - PREVIOUS_LINECOUNT)
	GOTO INPUT_LOOP
ENDIF

IF RESULT == 20000
	INDEX --
	SIF INDEX < 1
		INDEX = 1
	CLEARLINE (LINECOUNT - PREVIOUS_LINECOUNT)
	GOTO INPUT_LOOP
ELSEIF RESULT == 20001
	INDEX ++
	DEBUGPRINTFORML INDEX{INDEX}
	SIF INDEX > VARSIZE("COLUMN_ORDER")
		INDEX = VARSIZE("COLUMN_ORDER")
	CLEARLINE (LINECOUNT - PREVIOUS_LINECOUNT)
	GOTO INPUT_LOOP
ENDIF

RESULT -= 100
IF SHIP_COUNTRY:RESULT == PLAYER_COUNTRY
	FOR LOCAL, 0, CHARANUM
		IF BOARDING_SHIP_ID:LOCAL == RESULT
			PRINTFORML 搭乗者: %CALLNAME:LOCAL%
			BREAK
		ENDIF
	NEXT
	CALL SHOW_SHIP_INFO(RESULT)
	RETURN 0
ELSEIF RESULT >= 0 && RESULT < MAX_SHIP
	CALL SHOW_SHIP_INFO(RESULT)
	RETURN 0
ELSE
	RETURN 0
ENDIF

;-----------------------------------------------------
;船舶一覧の見出し行
;PRINTBUTTONで作ったボタンの整列ってできるの・・・？
;-----------------------------------------------------
@CREATE_HEADLINE_FOR_SHIP_LIST(INDEX, COLUMN_ORDER, DATA_WIDTH)
#DIM INDEX
#DIM REF COLUMN_ORDER
#DIM REF DATA_WIDTH
#DIM TOTAL_COLUMN_WIDTH

;オフセット分 + 先頭列
TOTAL_COLUMN_WIDTH = 2 + DATA_WIDTH:データ_艦名 

IF INDEX == 1
	SETCOLORBYNAME GRAY
	PRINTPLAIN <<
	RESETCOLOR
ELSE
	PRINTBUTTON "<<", 20000
ENDIF

;先頭行は固定
PRINTFORM %"艦名", DATA_WIDTH:データ_艦名, LEFT%
PRINT  

FOR LOCAL, INDEX, VARSIZE("COLUMN_ORDER")
	SIF LOCAL == 0
		CONTINUE
	;各列のデータ長+半角スペース
	TOTAL_COLUMN_WIDTH += DATA_WIDTH:(COLUMN_ORDER:LOCAL) + 1
	SIF TOTAL_COLUMN_WIDTH > RIGHT_COLUMN_LENGTH
		BREAK
	SELECTCASE COLUMN_ORDER:LOCAL
		CASE データ_艦名
			PRINTFORM %"艦名", DATA_WIDTH:データ_艦名, LEFT%
		CASE データ_艦種
			PRINTBUTTON "   艦種   ", 10000 + ソート_艦種
		CASE データ_所属
			PRINTBUTTON "      所属      ", 10000 + ソート_所属
		CASE データ_任務
			PRINTBUTTON "任務", 10000 + ソート_任務
		CASE データ_HP
			PRINTBUTTON "   耐久    ", 10000 + ソート_MAXHP
		CASE データ_シールド
			PRINTBUTTON "  シールド ", 10000 + ソート_MAXシールド
		CASE データ_実弾
			PRINTBUTTON "実弾", 10000 + ソート_実弾
		CASE データ_レーザー
			PRINTBUTTON "ﾚｰｻﾞｰ", 10000 + ソート_レーザー
		CASE データ_ミサイル
			PRINTBUTTON "ﾐｻｲﾙ", 10000 + ソート_ミサイル
		CASE データ_戦闘機
			PRINTBUTTON "航空", 10000 + ソート_戦闘機
		CASE データ_魚雷
			PRINTBUTTON "雷撃", 10000 + ソート_魚雷
		CASE データ_対空
			PRINTBUTTON "対空", 10000 + ソート_対空
		CASE データ_回避
			PRINTBUTTON "回避", 10000 + ソート_回避
	ENDSELECT
	PRINT  
NEXT

IF INDEX == VARSIZE("COLUMN_ORDER")
	SETCOLORBYNAME GRAY
	PRINTPLAIN >>
	RESETCOLOR
ELSE
	PRINTBUTTON ">>", 20001
ENDIF

PRINTFORML
;-----------------------------------------------------
;船舶一覧に表示する一行分の文字列を作成
;-----------------------------------------------------
@CREATE_COLUMN_FOR_SHIP_LIST(SHIP_ID, INDEX, COLUMN_ORDER, DATA_WIDTH)
#DIM SHIP_ID
#DIM INDEX
#DIM REF COLUMN_ORDER
#DIM REF DATA_WIDTH
#DIM TOTAL_COLUMN_WIDTH
#DIM FIREPOWER_LIST, 5

#DIMS COLUMN_DATA
CALL GET_TOTAL_FIREPOWER(SHIP_ID, FIREPOWER_LIST)

;先頭列の固定表示
TOTAL_COLUMN_WIDTH = 2 + DATA_WIDTH:データ_艦名
COLUMN_DATA =   %SHIP_NAME:(SHIP_ID), DATA_WIDTH:データ_艦名, LEFT%
COLUMN_DATA += "|"
FOR LOCAL, INDEX, VARSIZE("COLUMN_ORDER")
	;各列のデータ長+半角スペース
	SIF LOCAL == 0
		CONTINUE
	TOTAL_COLUMN_WIDTH += DATA_WIDTH:(COLUMN_ORDER:LOCAL) + 1
	SIF TOTAL_COLUMN_WIDTH > RIGHT_COLUMN_LENGTH
		BREAK
	SELECTCASE COLUMN_ORDER:LOCAL
		CASE データ_艦名
			COLUMN_DATA += @"%SHIP_NAME:(SHIP_ID), DATA_WIDTH:(COLUMN_ORDER:LOCAL), LEFT%"
		CASE データ_艦種
			COLUMN_DATA += @"%GET_TYPE_NAME(SHIP_CATEGORY:(SHIP_ID)), DATA_WIDTH:(COLUMN_ORDER:LOCAL), LEFT%"
		CASE データ_所属
			COLUMN_DATA += @"%COUNTRY_NAME:(SHIP_COUNTRY:(SHIP_ID)), DATA_WIDTH:(COLUMN_ORDER:LOCAL), LEFT%"
		CASE データ_任務
			COLUMN_DATA += @"%GET_SHIP_MISSION_STR(SHIP_ID), DATA_WIDTH:(COLUMN_ORDER:LOCAL), LEFT%"
		CASE データ_HP
			CALL GET_SHIP_HP(SHIP_ID)
			COLUMN_DATA += @"{RESULT, 5, RIGHT}"
			COLUMN_DATA += "/"
			COLUMN_DATA += @"{RESULT + (SHIP_DAMAGE:SHIP_ID), 5, RIGHT}"
		CASE データ_シールド
			CALL GET_SHIP_SIELD(SHIP_ID)
			COLUMN_DATA += @"{RESULT, 5, RIGHT}"
			COLUMN_DATA += "/"
			CALL GET_SHIP_TOTAL_SIELD(SHIP_ID)
			COLUMN_DATA += @"{RESULT, 5, RIGHT}"					
		CASE データ_実弾
			COLUMN_DATA += @"{FIREPOWER_LIST:実体弾, 4, RIGHT}"
		CASE データ_レーザー
			COLUMN_DATA += @"{FIREPOWER_LIST:レーザー, 4, RIGHT}"
		CASE データ_ミサイル
			COLUMN_DATA += @"{FIREPOWER_LIST:ミサイル, 4, RIGHT}"
		CASE データ_戦闘機
			COLUMN_DATA += @"{FIREPOWER_LIST:戦闘機, 4, RIGHT}"
		CASE データ_魚雷
			COLUMN_DATA += @"{FIREPOWER_LIST:魚雷, 4, RIGHT}" 
		CASE データ_対空
			CALL GET_SUB_ANTI_AIR(SHIP_ID)
			COLUMN_DATA += @"{RESULT, 4, RIGHT}" 
		CASE データ_回避
			CALL GET_AVOID(SHIP_ID)
			COLUMN_DATA += @"{RESULT, 4, RIGHT}" 
	ENDSELECT
	COLUMN_DATA += " " 
NEXT

RESULTS = %COLUMN_DATA%
;-------------------------------------------------
; CURRENT_SHIP_LISTのソート
;-------------------------------------------------
@SORT_STAR_PORT_SHIPS(CURRENT_SHIP_LIST)
#DIM REF CURRENT_SHIP_LIST
#DIM SORT_VALUE, MAX_SHIP
#DIM SORT_LIST, MAX_SHIP
#DIM COUNT_I
#DIM COUNT_J
#DIM COMPARED_A
#DIM COMPARED_B
#DIM SORT_END
#DIM FIREPOWER_LIST, 5
VARSET SORT_VALUE, -1
VARSET SORT_LIST, -1
SORT_END = 0
FOR LOCAL, 0, MAX_SHIP
	SIF CURRENT_SHIP_LIST:LOCAL == -1
		BREAK
	SELECTCASE ソートキー
		CASE ソート_NO
			SORT_VALUE:LOCAL = CURRENT_SHIP_LIST:LOCAL
		CASE ソート_所属
			SORT_VALUE:LOCAL = SHIP_COUNTRY:(CURRENT_SHIP_LIST:LOCAL)
		CASE ソート_艦種
			SORT_VALUE:LOCAL = SHIP_CATEGORY:(CURRENT_SHIP_LIST:LOCAL)
		CASE ソート_任務
			SORT_VALUE:LOCAL = SHIP_MISSION:(CURRENT_SHIP_LIST:LOCAL)
		CASE ソート_HP
			CALL GET_SHIP_HP(CURRENT_SHIP_LIST:LOCAL)
			SORT_VALUE:LOCAL = RESULT
		CASE ソート_MAXHP
			CALL GET_SHIP_TOTAL_HP(CURRENT_SHIP_LIST:LOCAL)
			SORT_VALUE:LOCAL = RESULT
		CASE ソート_HP割合
			CALL GET_SHIP_HP_PERCENT(CURRENT_SHIP_LIST:LOCAL)
			SORT_VALUE:LOCAL = RESULT
		CASE ソート_シールド
			CALL GET_SHIP_SIELD(CURRENT_SHIP_LIST:LOCAL)
			SORT_VALUE:LOCAL = RESULT
		CASE ソート_MAXシールド
			CALL GET_SHIP_TOTAL_SIELD(CURRENT_SHIP_LIST:LOCAL)
			SORT_VALUE:LOCAL = RESULT
		CASE ソート_シールド割合
			CALL GET_SHIP_SHIELD_PERCENT(CURRENT_SHIP_LIST:LOCAL)
			SORT_VALUE:LOCAL = RESULT
		CASE ソート_実弾
			CALL GET_TOTAL_FIREPOWER(CURRENT_SHIP_LIST:LOCAL, FIREPOWER_LIST)
			SORT_VALUE:LOCAL = FIREPOWER_LIST:実体弾
		CASE ソート_レーザー
			CALL GET_TOTAL_FIREPOWER(CURRENT_SHIP_LIST:LOCAL, FIREPOWER_LIST)
			SORT_VALUE:LOCAL = FIREPOWER_LIST:レーザー
		CASE ソート_ミサイル
			CALL GET_TOTAL_FIREPOWER(CURRENT_SHIP_LIST:LOCAL, FIREPOWER_LIST)
			SORT_VALUE:LOCAL = FIREPOWER_LIST:ミサイル
		CASE ソート_戦闘機
			CALL GET_TOTAL_FIREPOWER(CURRENT_SHIP_LIST:LOCAL, FIREPOWER_LIST)
			SORT_VALUE:LOCAL = FIREPOWER_LIST:戦闘機
		CASE ソート_魚雷
			CALL GET_TOTAL_FIREPOWER(CURRENT_SHIP_LIST:LOCAL, FIREPOWER_LIST)
			SORT_VALUE:LOCAL = FIREPOWER_LIST:魚雷
		CASE ソート_対空
			CALL GET_SUB_ANTI_AIR(CURRENT_SHIP_LIST:LOCAL)
			SORT_VALUE:LOCAL = RESULT
		CASE ソート_回避
			CALL GET_AVOID(CURRENT_SHIP_LIST:LOCAL)
			SORT_VALUE:LOCAL = RESULT
	ENDSELECT
	SORT_LIST:LOCAL = CURRENT_SHIP_LIST:LOCAL
	SORT_END ++
NEXT

SIF SORT_END < 2
	RETURN 1

FOR COUNT_I, 0, SORT_END
	FOR COUNT_J, SORT_END - 1, COUNT_I, -1
		COMPARED_A = COUNT_J - 1
		COMPARED_B = COUNT_J
		;DEBUGPRINTFORML {COUNT_J-1}-{COUNT_J} {SORT_VALUE:(COMPARED_A)} vs {SORT_VALUE:(COMPARED_B)}
		IF (降順ソート && SORT_VALUE:(COMPARED_A) < SORT_VALUE:(COMPARED_B)) || (!降順ソート && SORT_VALUE:(COMPARED_A) > SORT_VALUE:(COMPARED_B))
			SWAP SORT_LIST:COUNT_J, SORT_LIST:(COUNT_J-1)
			SWAP SORT_VALUE:COUNT_J, SORT_VALUE:(COUNT_J-1)
		ENDIF
	NEXT	
NEXT
;DEBUGPRINTFORML ------
FOR LOCAL, 0, SORT_END
	CURRENT_SHIP_LIST:LOCAL = SORT_LIST:LOCAL
	;DEBUGPRINTFORM %SHIP_NAME:(CURRENT_SHIP_LIST:LOCAL)%
	;DEBUGPRINTFORML {SORT_VALUE:LOCAL}
NEXT

;-------------------------------------------------
; DRAW RIGHT COLUMN GRAY LINE
;-------------------------------------------------
@DRAW_RIGHT_COLUMN_GRAY_LINE
#DIM ITER

SETCOLOR 0X666666
FOR ITER, 0, RIGHT_COLUMN_LENGTH
	PRINT -
NEXT
RESETCOLOR
PRINTL

;-------------------------------------------------
; PRINT LEFT COLUMN
;-------------------------------------------------
@PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
#DIMS REF LEFT_COLUMNS
#DIM REF LEFT_COLUMNS_BUTTON_VALUE
#DIM LINE_ITER
IF LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER == NONE_LINE
	PRINTFORM %"", LEFT_COLUMN_LENGTH, LEFT%
ELSEIF LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER == WHITE_LINE
	PRINTFORM %LEFT_COLUMNS:LINE_ITER, LEFT_COLUMN_LENGTH, LEFT%
ELSEIF LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER == GRAY_LINE
	SETCOLOR 0X666666
	PRINTFORM %LEFT_COLUMNS:LINE_ITER, LEFT_COLUMN_LENGTH, LEFT%
	RESETCOLOR
ELSEIF LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER == GREEN_LINE
	SETCOLOR 0X00FF00
	PRINTFORM %LEFT_COLUMNS:LINE_ITER, LEFT_COLUMN_LENGTH, LEFT%
	RESETCOLOR
ELSE
	PRINTBUTTON @"%LEFT_COLUMNS:LINE_ITER%", LEFT_COLUMNS_BUTTON_VALUE:LINE_ITER
ENDIF

;-------------------------------------------------
; 艦船購入メニュー
;-------------------------------------------------
@SLG_SHIP_SHOP(PORT_POSID, OWNER, LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE)
#DIM PORT_POSID
#DIM OWNER
#DIMS REF LEFT_COLUMNS
#DIM REF LEFT_COLUMNS_BUTTON_VALUE
#DIM LINE_ITER
#DIM DUMMY
#DIM LEFT_CLUMN_SIZE
#DIM SHIP_STATUS, SHIP_STATUS_LENGTH
#DIM SHIP_MODEL_ID
#DIM SELECTED

FINDELEMENT LEFT_COLUMNS_BUTTON_VALUE, NONE_LINE
LEFT_CLUMN_SIZE = RESULT
$INPUT_LOOP
LINE_ITER = 0

CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
IF MONEY == 0
	LOCALS = $0
ELSE
	LOCALS = %TOSTR(MONEY, "$###,###,###,###")%
ENDIF
PRINTFORML 　艦船の購入 - 所持金: %LOCALS%
LINE_ITER ++

CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
CALL DRAW_RIGHT_COLUMN_GRAY_LINE
LINE_ITER ++

FOR SHIP_MODEL_ID, 0, 50
	TRYCCALLFORM GET_SHIP_STATUS_{SHIP_MODEL_ID}(SHIP_STATUS)
		RESULT = 1
		TRYCALLFORM IS_SALE_SHIP_{SHIP_MODEL_ID}
		SIF RESULT == 0
			CONTINUE
		IF OWNER == PLAYER_COUNTRY
			CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
			TRYCALLFORM GET_SHIP_NAME_{SHIP_MODEL_ID}
			PRINTBUTTON @"　%RESULTS, 20, LEFT% $0", SHIP_MODEL_ID + 100
		ELSE
			CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
			TRYCALLFORM GET_SHIP_STATUS_{SHIP_MODEL_ID}(SHIP_STATUS)
			TRYCALLFORM GET_SHIP_NAME_{SHIP_MODEL_ID}
			LOCALS = %TOSTR(SHIP_STATUS:SHIP_PRICE, "$##,###,###,###")%
			PRINTBUTTON @"　 %RESULTS, 20, LEFT% %LOCALS, 12, RIGHT%", SHIP_MODEL_ID + 100
		ENDIF
		LINE_ITER ++
		PRINTL
	CATCH
		CONTINUE
	ENDCATCH
NEXT

INPUT
IF RESULT >= 0 && RESULT < LEFT_CLUMN_SIZE
	RETURN RESULT
ENDIF
RESULT -= 100

SELECTED = RESULT
TRYCCALLFORM GET_SHIP_NAME_{SELECTED}
	RESULT = 1
	TRYCALLFORM IS_SALE_{SELECTED}
	SIF RESULT == 0
		GOTO INPUT_LOOP
	TRYCALLFORM GET_SHIP_DESCRIPTION_{SELECTED}
	CALL BUILD_SHIP(PORT_POSID, OWNER, PLAYER_COUNTRY, SELECTED, RESULTS, DUMMY)
	;,
CATCH
	GOTO INPUT_LOOP
ENDCATCH
;-------------------------------------------------
; 装備更新
;-------------------------------------------------
@SLG_EQUIP_SHOP(PORTPOSID, OWNER, LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE)
#DIM PORTPOSID
#DIM OWNER
#DIMS REF LEFT_COLUMNS
#DIM REF LEFT_COLUMNS_BUTTON_VALUE
#DIM SHIP_ID
#DIM SELECTED
#DIM LINE_ITER
#DIM LEFT_COLUMN_SIZE
#DIM SLOT_ID
#DIM SHIP_STATUS, SHIP_STATUS_LENGTH

FINDELEMENT LEFT_COLUMNS_BUTTON_VALUE, NONE_LINE
LEFT_COLUMN_SIZE = RESULT

$INPUT_LOOP

LINE_ITER = 0
CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
PRINTL 　装備更新 - 古い装備は破棄されます
LINE_ITER ++

CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
CALL DRAW_RIGHT_COLUMN_GRAY_LINE
LINE_ITER ++


FOR SHIP_ID, 0, MAX_SHIP
	SIF SHIP_COUNTRY:SHIP_ID != PLAYER_COUNTRY
		CONTINUE
	SIF SHIP_POSITION:SHIP_ID != PORTPOSID
		CONTINUE
	CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
	PRINTBUTTON @"[{SHIP_ID, 3, RIGHT}] %SHIP_NAME:SHIP_ID, 30, LEFT%", SHIP_ID + 100
	PRINTL
	LINE_ITER ++
NEXT

; 左カラムが右絡むより長かった場合の処理
FOR LOCAL, LINE_ITER, LEFT_COLUMN_SIZE
	CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
	PRINTL
	LINE_ITER ++
NEXT
PRINTFORM %"", LEFT_COLUMN_LENGTH, LEFT%
CALL DRAW_RIGHT_COLUMN_GRAY_LINE

INPUT

IF RESULT >= 0 && RESULT < LEFT_COLUMN_SIZE
	RETURN RESULT
ENDIF


SHIP_ID = RESULT - 100
IF SHIP_ID >= 0 && SHIP_ID < MAX_SHIP && SHIP_COUNTRY:SHIP_ID == PLAYER_COUNTRY && SHIP_POSITION:SHIP_ID == PORTPOSID
	$INPUT_LOOP2
	TRYCALLFORM GET_SHIP_STATUS_{SHIP_MODEL:SHIP_ID}(SHIP_STATUS)
	RESULTS = %NO_ITEM%
	PRINTBUTTON @"%"名称変更", 20, LEFT% %SHIP_NAME:SHIP_ID%", 0
	PRINTL
	SETCOLOR 0XFF7F7F
	FOR SLOT_ID, 0, SHIP_STATUS:SHIP_MAIN_WEAPON_SLOT
		TRYCALLFORM GET_MAIN_WEAPON_NAME_{SHIP_EQUIP:主砲:SLOT_ID:SHIP_ID}
		LOCALS = 主砲{SLOT_ID + 1}変更
		PRINTBUTTON @"%LOCALS, 20, LEFT% %RESULTS%", 10 + SLOT_ID
		PRINTL
	NEXT
	RESETCOLOR
	SETCOLOR 0XFF7FFF
	FOR SLOT_ID, 0, SHIP_STATUS:SHIP_SUB_WEAPON_SLOT
		TRYCALLFORM GET_SUB_WEAPON_NAME_{SHIP_EQUIP:副砲:SLOT_ID:SHIP_ID}
		LOCALS = 副砲{SLOT_ID + 1}変更
		PRINTBUTTON @"%LOCALS, 20, LEFT% %RESULTS%", 20 + SLOT_ID
		PRINTL
	NEXT
	RESETCOLOR
	SETCOLOR 0XBF7FFF
	FOR SLOT_ID, 0, SHIP_STATUS:SHIP_SIELD_SLOT
		TRYCALLFORM GET_SIELD_NAME_{SHIP_EQUIP:シールド:SLOT_ID:SHIP_ID}
		LOCALS = シールド{SLOT_ID + 1}変更
		PRINTBUTTON @"%LOCALS, 20, LEFT% %RESULTS%", 30 + SLOT_ID
		PRINTL
	NEXT
	RESETCOLOR
	SETCOLOR 0X7FBFFF
	FOR SLOT_ID, 0, SHIP_STATUS:SHIP_ARMOR_SLOT
		TRYCALLFORM GET_ARMOR_NAME_{SHIP_EQUIP:装甲:SLOT_ID:SHIP_ID}
		LOCALS = 装甲{SLOT_ID + 1}変更
		PRINTBUTTON @"%LOCALS, 20, LEFT% %RESULTS%", 40 + SLOT_ID
		PRINTL
	NEXT
	RESETCOLOR
	SETCOLOR 0X7FFFFF
	TRYCALLFORM GET_ENGIN_NAME_{SHIP_EQUIP:エンジン:0:SHIP_ID}
	PRINTBUTTON @"%"エンジン変更", 20, LEFT% %RESULTS%", 5
	PRINTL
	RESETCOLOR
	SETCOLOR 0X7FFFBF
	TRYCALLFORM GET_COMPUTER_NAME_{SHIP_EQUIP:AI:0:SHIP_ID}
	PRINTBUTTON @"%"AI変更", 20, LEFT% %RESULTS%", 6
	PRINTL
	RESETCOLOR
	SETCOLOR 0XFFFF7F
	TRYCALLFORM GET_SENSOR_NAME_{SHIP_EQUIP:センサー:0:SHIP_ID}
	PRINTBUTTON @"%"センサー変更", 20, LEFT% %RESULTS%", 7
	PRINTL
	RESETCOLOR
	SETCOLOR 0XFFBF7F
	RESULTS = %NO_ITEM%
	TRYCALLFORM GET_OPTION_NAME_{SHIP_EQUIP:オプション:0:SHIP_ID}
	PRINTBUTTON @"%"オプション", 20, LEFT% %RESULTS%", 8
	PRINTL
	RESETCOLOR
	PRINTBUTTON "[9] 戻る", 9
ELSEIF RESULT == 9999
	RETURN
ELSE
	GOTO INPUT_LOOP
ENDIF
INPUT
SELECTCASE RESULT
	CASE 0
		CALL CHANGE_SHIP_NAME(SHIP_ID)
		GOTO INPUT_LOOP2
	CASE 10 TO SHIP_STATUS:SHIP_MAIN_WEAPON_SLOT - 1 + 10
		CALL EQUIP_UPDATE_0(SHIP_ID, OWNER, RESULT % 10)
		GOTO INPUT_LOOP2
	CASE 20 TO SHIP_STATUS:SHIP_SUB_WEAPON_SLOT - 1 + 20
		CALL EQUIP_UPDATE_1(SHIP_ID, OWNER, RESULT % 20)
		GOTO INPUT_LOOP2
	CASE 30 TO SHIP_STATUS:SHIP_SIELD_SLOT - 1 + 30
		CALL EQUIP_UPDATE_2(SHIP_ID, OWNER, RESULT % 30)
		GOTO INPUT_LOOP2
	CASE 40 TO SHIP_STATUS:SHIP_ARMOR_SLOT - 1 + 40
		CALL EQUIP_UPDATE_3(SHIP_ID, OWNER, RESULT % 40)
		GOTO INPUT_LOOP2
	CASE 5
		CALL EQUIP_UPDATE_4(SHIP_ID, OWNER)
		GOTO INPUT_LOOP2
	CASE 6
		CALL EQUIP_UPDATE_5(SHIP_ID, OWNER)
		GOTO INPUT_LOOP2
	CASE 7
		CALL EQUIP_UPDATE_6(SHIP_ID, OWNER)
		GOTO INPUT_LOOP2
	CASE 8
		CALL EQUIP_UPDATE_7(SHIP_ID, OWNER)
		GOTO INPUT_LOOP2
	CASE 9
		GOTO INPUT_LOOP
	CASEELSE
		GOTO INPUT_LOOP2
ENDSELECT

;-------------------------------------------------
; 入港可能かチェック
;-------------------------------------------------
@CHECK_ARRIVAL(SHIP_ID)
#FUNCTION
#DIM SHIP_ID
SIF SHIP_ID < 0 || SHIP_ID >= MAX_SHIP
	RETURNF 0
FOR LOCAL, 0, 星系数
	IF 星系座標ID:LOCAL == SHIP_POSITION:SHIP_ID
		; TODO 敵対チェックを入れる
		RETURNF 1
	ENDIF
NEXT
RETURNF 0

;-------------------------------------------------
; ターンエンド
;-------------------------------------------------
@SLG_TURN_END(IS_TIME_PROGRESS)
#DIM IS_TIME_PROGRESS
#DIM POS_X
#DIM POS_Y
#DIM ITER
#DIM STAR_ID
; 星ごとの民需生産と消費
FOR STAR_ID, 0, 星系数
	CALL PRICATE_PRODUCTION(STAR_ID)
	CALL PRIVATE_DEMAND(STAR_ID)
NEXT


CALL BEGIN_SLG_EVENT
PRINTFORML AI試行中……
CALL SLG_AI
CALL PATH_WARP_GATE
CALL UPDATE_CHARACTER_POSITION

VARSET CHECKED_MAP, 0

; シールドの自動回復
FOR ITER, 0, MAX_SHIP
	SIF SHIP_COUNTRY:ITER > 0
		CALL AUTO_RECOVER_SIELD(ITER)
NEXT
; DOCKにいる艦船の回復
CALL DOCK_REPAIR
IF IS_TIME_PROGRESS == 1
	TIME ++
	IF TIME % 2 == 0
		DAY ++
		TIME = 0
	ENDIF
ENDIF
RETURN

;-------------------------------------------------
; MAPID2PORTID
;-------------------------------------------------
@MAPID2PORTID(MAPID)
#DIM MAPID

FOR LOCAL, 0, 星系数
	SIF 星系座標ID:LOCAL == MAPID
		RETURN LOCAL
NEXT

;-------------------------------------------------
; キャラクターの現在位置更新
;-------------------------------------------------
@UPDATE_CHARACTER_POSITION
#DIM ID

; キャラクターの移動(船に乗っているキャラクターは労働しない)
FOR ID, 0, CHARANUM
	IF BOARDING_SHIP_ID:ID != -1
		CHARACTER_POSITION:ID = SHIP_POSITION:(BOARDING_SHIP_ID:ID)
		MASTER_LEFT_DAY:ID = -1
	ELSEIF RIDING_WITH:ID != -1
		CHARACTER_POSITION:ID = SHIP_POSITION:(RIDING_WITH:ID)
		MASTER_LEFT_DAY:ID = -1
	ENDIF
NEXT

SIF TARGET != -1 && CHARACTER_POSITION:0 != CHARACTER_POSITION:TARGET
	TARGET = -1